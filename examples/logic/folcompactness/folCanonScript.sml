open HolKernel Parse boolLib bossLib;

open mp_then

open pred_setTheory set_relationTheory listTheory
open folSkolemTheory folPrenexTheory folModelsTheory folLangTheory
     folPropTheory

fun f $ x = f x

val _ = new_theory "folCanon";

(* ========================================================================= *)
(* Canonical models in FOL and their use to derive classic metatheorems.     *)
(* ========================================================================= *)

(* ------------------------------------------------------------------------- *)
(* Domain of a canonical model for given language.                           *)
(* ------------------------------------------------------------------------- *)

Inductive terms:
  (âˆ€x. terms fns (V x)) âˆ§
  (âˆ€f l. (f,LENGTH l) âˆˆ fns âˆ§ (âˆ€t. MEM t l â‡’ terms fns t) â‡’
         terms fns (Fn f l))
End

val stupid_canondom_lemma = Q.prove(
  â€˜âˆ€t. t âˆˆ terms (FST L) â‡’ term_functions t âŠ† FST Lâ€™,
  simp[IN_DEF] >> Induct_on â€˜termsâ€™ >>
  dsimp[SUBSET_DEF, MEM_MAP] >> metis_tac[]);

Theorem finite_subset_instance:
  âˆ€t'. FINITE t' âˆ§ t' âŠ† { formsubst v p | P v âˆ§ p âˆˆ s} â‡’
        âˆƒt. FINITE t âˆ§ t âŠ† s âˆ§ t' âŠ† {formsubst v p | P v âˆ§ p âˆˆ t}
Proof
  Induct_onâ€˜FINITEâ€™ >> simp[] >> rw[]
  >- (qexists_tac â€˜âˆ…â€™ >> simp[]) >> fs[] >>
  rename [â€˜p âˆˆ sâ€™, â€˜formsubst v p âˆ‰ t'â€™, â€˜t âŠ† sâ€™] >>
  qexists_tac â€˜p INSERT tâ€™ >> simp[] >> conj_tac >- metis_tac[] >>
  fs[SUBSET_DEF] >>metis_tac[]
QED

Theorem finite_subset_SKOLEM:
  âˆ€s u. FINITE u âˆ§ u âŠ† { SKOLEM p | p âˆˆ s} â‡’
        âˆƒt. FINITE t âˆ§ t âŠ† s âˆ§ u = {SKOLEM p | p âˆˆ t}
Proof
  Induct_on â€˜FINITEâ€™ >> simp[] >> rw[]
  >- (qexists_tac â€˜âˆ…â€™ >> simp[]) >>
  first_x_assum (drule_then (qx_choose_then â€˜ttâ€™ strip_assume_tac)) >> rw[] >>
  rename [â€˜p âˆˆ sâ€™] >> qexists_tac â€˜p INSERT ttâ€™ >> simp[] >>
  dsimp[EXTENSION]
QED

Theorem valuation_exists:
  âˆ€M. M.Dom â‰  âˆ… â‡’ âˆƒv:num -> Î±. valuation M v
Proof
  simp[EXTENSION, PULL_EXISTS, valuation_def] >> qx_genl_tac [â€˜Mâ€™, â€˜xâ€™] >>
  strip_tac >> qexists_tac â€˜K xâ€™ >> simp[]
QED

Theorem holds_FOLDR_exists:
  âˆ€M xs v.
    holds M v (FOLDR Exists p xs) â‡”
    âˆƒas. LENGTH as = LENGTH xs âˆ§ EVERY M.Dom as âˆ§
         holds M (FOLDL (combin$C (UNCURRY UPDATE)) v (MAP2 $, xs as)) p
Proof
  Induct_on â€˜xsâ€™ >> simp[PULL_EXISTS] >> rpt gen_tac >> eq_tac
  >- (disch_then (qx_choosel_then [â€˜aâ€™, â€˜asâ€™] strip_assume_tac) >>
      qexists_tac â€˜a::asâ€™ >> simp[] >> fs[IN_DEF]) >>
  simp[PULL_EXISTS] >> Cases >> simp[] >> rw[] >>
  rename [â€˜LENGTH as = LENGTH xsâ€™, â€˜M.Dom aâ€™] >>
  map_every qexists_tac [â€˜aâ€™, â€˜asâ€™] >> simp[] >> simp[IN_DEF]
QED

(* ------------------------------------------------------------------------- *)
(* Canonical model based on the language of a set of formulas.               *)
(* ------------------------------------------------------------------------- *)

Definition canonical_def:
  canonical L M â‡” M.Dom = terms (FST L) âˆ§ âˆ€f. M.Fun f = Fn f
End

(* ------------------------------------------------------------------------- *)
(* Mappings between models and propositional valuations.                     *)
(* ------------------------------------------------------------------------- *)

Definition prop_of_model_def:
  prop_of_model M v (Pred p l) â‡” holds M v (Pred p l)
End

Definition canon_of_prop_def:
  canon_of_prop L v = <| Dom := terms (FST L); Fun := Fn;
                         Pred := Î»p zs. v (Pred p zs) |>
End

Theorem pholds_prop_of_model:
  âˆ€p. qfree p â‡’ (pholds (prop_of_model M v) p â‡” holds M v p)
Proof
  Induct >> simp[prop_of_model_def]
QED

Theorem prop_of_canon_of_prop:
  âˆ€p l. prop_of_model (canon_of_prop L v) V (Pred p l) = v (Pred p l)
Proof
  simp[prop_of_model_def, canon_of_prop_def] >> rpt gen_tac >>
  rpt AP_TERM_TAC >>
  qmatch_abbrev_tac â€˜MAP f l = lâ€™ >>
  â€˜âˆ€t. f t = tâ€™ suffices_by simp[LIST_EQ_REWRITE, EL_MAP] >>
  Induct >> simp[Abbrâ€˜fâ€™, Cong MAP_CONG]
QED

Theorem holds_canon_of_prop:
  qfree p â‡’ (holds (canon_of_prop L v) V p â‡” pholds v p)
Proof
  strip_tac >>
  drule_then (rewrite_tac o Portable.single o GSYM) pholds_prop_of_model >>
  Induct_on â€˜pâ€™ >> simp[prop_of_canon_of_prop]
QED

Theorem holds_canon_of_prop_general:
  qfree p â‡’ (holds (canon_of_prop L v1) v2 p â‡” pholds v1 (formsubst v2 p))
Proof
  strip_tac >>
  drule_then (rewrite_tac o Portable.single o GSYM) pholds_prop_of_model >>
  Induct_on â€˜pâ€™ >> simp[] >>
  simp[prop_of_model_def, canon_of_prop_def] >>
  simp[Cong MAP_CONG, GSYM termsubst_termval]
QED

Theorem canonical_canon_of_prop:
  canonical L (canon_of_prop L d)
Proof
  simp[canonical_def, canon_of_prop_def]
QED

Theorem interpretation_canon_of_prop:
  âˆ€L d. interpretation L (canon_of_prop L d)
Proof
  simp[interpretation_def, canon_of_prop_def, pairTheory.FORALL_PROD] >>
  metis_tac[terms_rules,IN_DEF]
QED

(* ------------------------------------------------------------------------- *)
(* Equivalence theorems for tautologies.                                     *)
(* ------------------------------------------------------------------------- *)

Theorem prop_valid_imp_fol_valid:
  âˆ€p. qfree p âˆ§ (âˆ€d. pholds d p) â‡’ âˆ€M v. holds M v p
Proof
  metis_tac[pholds_prop_of_model]
QED

Theorem fol_valid_imp_prop_valid:
  !p. qfree p âˆ§
      (âˆ€C (v:num->term). canonical(language {p}) C â‡’ holds C v p)
       ==> !d. pholds d p
Proof
  metis_tac[canonical_canon_of_prop, holds_canon_of_prop]
QED

(* ------------------------------------------------------------------------- *)
(* Same thing for satisfiability.                                            *)
(* ------------------------------------------------------------------------- *)

Theorem satisfies_psatisfies:
  (âˆ€p. p âˆˆ s â‡’ qfree p) âˆ§ M satisfies s âˆ§ valuation M v â‡’
  prop_of_model M v psatisfies s
Proof
  simp[satisfies_def, psatisfies_def] >> metis_tac[pholds_prop_of_model]
QED

Theorem psatisfies_instances:
  (âˆ€p. p âˆˆ s â‡’ qfree p) âˆ§
  d psatisfies {formsubst v p | (âˆ€x. v x âˆˆ terms (FST L)) âˆ§ p âˆˆ s} â‡’
  canon_of_prop L d satisfies s
Proof
  simp[satisfies_def, psatisfies_def, PULL_EXISTS] >> strip_tac >>
  simp[SimpL â€œ$==>â€, valuation_def, canon_of_prop_def] >> rpt strip_tac >>
  â€˜pholds d (formsubst v p)â€™ by metis_tac[] >>
  â€˜holds (canon_of_prop L d) V (formsubst v p)â€™
    by metis_tac[holds_canon_of_prop, qfree_formsubst] >>
  fs[holds_formsubst] >>
  â€˜termval (canon_of_prop L d) V o v = vâ€™ suffices_by (strip_tac >> fs[]) >>
  simp[FUN_EQ_THM] >> simp[termval_triv, canon_of_prop_def]
QED

Theorem satisfies_instances:
  interpretation (language t) M â‡’
  (M satisfies {formsubst i p | p âˆˆ s âˆ§ âˆ€x. i x âˆˆ terms (FST (language t))} â‡”
   M satisfies s)
Proof
  simp[satisfies_def, PULL_EXISTS] >> rpt strip_tac >> eq_tac >> rpt strip_tac
  >- metis_tac[formsubst_triv, IN_DEF, terms_rules] >>
  simp[holds_formsubst] >> first_assum irule >> simp[valuation_def] >>
  gen_tac >> irule interpretation_termval >> simp[] >>
  qexists_tac â€˜predicates tâ€™ >>
  irule interpretation_sublang >> fs[language_def] >>
  metis_tac[stupid_canondom_lemma, pairTheory.FST]
QED

(* ------------------------------------------------------------------------- *)
(* So we have compactness in a strong form.                                  *)
(* ------------------------------------------------------------------------- *)

Theorem COMPACT_CANON_QFREE:
  (âˆ€p. p âˆˆ s â‡’ qfree p) âˆ§
  (âˆ€t. FINITE t âˆ§ t âŠ† s â‡’
       âˆƒM. interpretation (language ss) M âˆ§ M.Dom â‰  âˆ… âˆ§ M satisfies t) â‡’
  âˆƒC. interpretation (language ss) C âˆ§ canonical (language ss) C âˆ§
      C satisfies s
Proof
  rpt strip_tac >>
  â€˜psatisfiable
     { formsubst v p | (âˆ€x. v x âˆˆ terms (FST (language ss))) âˆ§ p âˆˆ s }â€™
    by (rewrite_tac[psatisfiable_def, psatisfies_def] >> irule COMPACT_PROP >>
        simp[GSYM psatisfies_def, GSYM psatisfiable_def] >>
        qx_gen_tac â€˜uâ€™ >> strip_tac >>
        â€˜âˆƒt. FINITE t âˆ§ t âŠ† s âˆ§
             u âŠ† {formsubst v p |
                   (âˆ€x. v x âˆˆ terms (FST (language ss))) âˆ§ p âˆˆ t }â€™
           by (ho_match_mp_tac finite_subset_instance >> simp[]) >>
        irule psatisfiable_mono >>
        goal_assum (first_assum o mp_then Any mp_tac) >>
        first_x_assum (drule_all_then strip_assume_tac) >>
        drule_then (qx_choose_then â€˜vâ€™ strip_assume_tac) valuation_exists >>
        drule_then strip_assume_tac satisfies_instances >>
        â€˜âˆ€p. p âˆˆ t â‡’ qfree pâ€™ by metis_tac[SUBSET_DEF] >>
        rewrite_tac[psatisfiable_def, psatisfies_def] >>
        qexists_tac â€˜prop_of_model M vâ€™ >> simp[GSYM psatisfies_def] >>
        irule satisfies_psatisfies >> simp[PULL_EXISTS] >>
        fs[AC CONJ_ASSOC CONJ_COMM]) >>
  fs[psatisfiable_def] >> rename [â€˜d psatisfies _â€™] >>
  qexists_tac â€˜canon_of_prop (language ss) dâ€™ >>
  simp[canonical_canon_of_prop, interpretation_canon_of_prop,
       psatisfies_instances]
QED

(* ------------------------------------------------------------------------- *)
(* Tedious lemma about sublanguage.                                          *)
(* ------------------------------------------------------------------------- *)

Theorem interpretation_restrictlanguage:
  âˆ€M s t. t âŠ† s âˆ§ interpretation (language s) M â‡’ interpretation (language t) M
Proof
  rw[language_def] >> irule interpretation_sublang >>
  goal_assum (first_assum o mp_then Any mp_tac) >>
  simp[functions_def, SUBSET_DEF, PULL_EXISTS] >> metis_tac[SUBSET_DEF]
QED

Theorem interpretation_extendlanguage:
  âˆ€M s t. interpretation (language t) M âˆ§ M.Dom â‰  âˆ… âˆ§ M satisfies t â‡’
          âˆƒM'. M'.Dom = M.Dom âˆ§ M'.Pred = M.Pred âˆ§
               interpretation (language s) M' âˆ§ M' satisfies t
Proof
  rpt strip_tac >>
  qexists_tac â€˜M with Fun := Î»g zs. if (g,LENGTH zs) âˆˆ functions t then
                                      M.Fun g zs
                                    else @a. a âˆˆ M.Domâ€™ >>
  simp[] >> conj_tac
  >- (fs[interpretation_def, language_def] >> rw[] >> SELECT_ELIM_TAC >>
      fs[EXTENSION] >> metis_tac[]) >>
  pop_assum mp_tac >> simp[satisfies_def, valuation_def] >>
  rpt strip_tac >> qmatch_abbrev_tac â€˜holds M' v pâ€™ >>
  â€˜âˆ€v. holds M v p â‡” holds M' v pâ€™ suffices_by metis_tac[] >>
  irule holds_functions >> simp[Abbrâ€˜M'â€™] >> simp[functions_def, PULL_EXISTS] >>
  rw[] >> metis_tac[]
QED

(* ------------------------------------------------------------------------- *)
(* Generalize to any formulas, via Skolemization.                            *)
(* ------------------------------------------------------------------------- *)

Theorem COMPACT_LS:
  (âˆ€t. FINITE t âˆ§ t âŠ† s â‡’
       âˆƒM. interpretation (language s) M âˆ§ M.Dom â‰  âˆ… âˆ§ M satisfies t)
 â‡’
  âˆƒC:term model. C.Dom â‰  âˆ… âˆ§ interpretation (language s) C âˆ§ C satisfies s
Proof
  strip_tac >>
  â€˜âˆ€u. FINITE u âˆ§ u âŠ† {SKOLEM p | p âˆˆ s} â‡’
       âˆƒM. interpretation (language {SKOLEM p | p âˆˆ s }) M âˆ§
           M.Dom â‰  âˆ… âˆ§ M satisfies uâ€™
    by (rpt strip_tac >>
        â€˜âˆƒt. u = {SKOLEM p | p âˆˆ t} âˆ§ FINITE t âˆ§ t âŠ† sâ€™
           by metis_tac[finite_subset_SKOLEM] >> rw[] >>
        first_x_assum (drule_all_then (qx_choose_then â€˜Mâ€™ strip_assume_tac))>>
        Q.SUBGOAL_THEN â€˜âˆƒM. M.Dom â‰  âˆ… âˆ§ interpretation (language t) M âˆ§
                            M satisfies tâ€™
           (qx_choose_then â€˜M0â€™ strip_assume_tac o
            ONCE_REWRITE_RULE[SKOLEM_SATISFIABLE])
        >- (qexists_tac â€˜Mâ€™ >> simp[] >>
            irule interpretation_restrictlanguage >> metis_tac[]) >>
        metis_tac[interpretation_extendlanguage]) >>
  â€˜âˆ€q. q âˆˆ {SKOLEM p | p âˆˆ s} â‡’ qfree qâ€™ by simp[PULL_EXISTS] >>
  drule_all_then (qx_choose_then â€˜CMâ€™ strip_assume_tac) COMPACT_CANON_QFREE >>
  simp[Once SKOLEM_SATISFIABLE] >> qexists_tac â€˜CMâ€™ >> simp[] >>
  fs[canonical_def] >> simp[EXTENSION] >> simp[IN_DEF] >>
  metis_tac[terms_rules]
QED

(* ------------------------------------------------------------------------- *)
(* Split away the compactness argument.                                      *)
(* ------------------------------------------------------------------------- *)

Theorem CANON:
 âˆ€M:Î± model s.
    interpretation (language s) M âˆ§
    M.Dom â‰  âˆ… âˆ§ (âˆ€p. p IN s â‡’ qfree p) /\ M satisfies s
  â‡’
    âˆƒC:term model.
        C.Dom â‰  âˆ… âˆ§ interpretation (language s) C âˆ§ C satisfies s
Proof
  rpt strip_tac >> irule COMPACT_LS THEN
  metis_tac[satisfies_def, SUBSET_DEF]
QED

(* ------------------------------------------------------------------------- *)
(* Conventional form of the LS theorem.                                      *)
(* ------------------------------------------------------------------------- *)

Definition LOWMOD_def:
  LOWMOD M = <| Dom := { num_of_term t | t âˆˆ M.Dom } ;
                Fun := Î»g zs. num_of_term (M.Fun g (MAP term_of_num zs)) ;
                Pred := Î»p zs. M.Pred p (MAP term_of_num zs) |>
End

Theorem DOM_LOWMOD_EMPTY[simp]:
  (LOWMOD M).Dom = âˆ… â‡” M.Dom = âˆ…
Proof
  simp[LOWMOD_def, EXTENSION]
QED

Theorem LOWMOD_termval:
  valuation (LOWMOD M) v â‡’
  âˆ€t. termval (LOWMOD M) v t = num_of_term (termval M (term_of_num o v) t)
Proof
  simp[SimpL â€œ$==>â€, valuation_def, LOWMOD_def] >> strip_tac >>
  Induct >> simp[Cong MAP_CONG] >- metis_tac[TERM_OF_NUM] >>
  simp[LOWMOD_def, MAP_MAP_o, Cong MAP_CONG]
QED

Theorem term_of_num_composition:
  term_of_num o vâ¦‡ n â†¦ num_of_term t â¦ˆ = (term_of_num o v)â¦‡ n â†¦ t â¦ˆ
Proof
  simp[FUN_EQ_THM, combinTheory.APPLY_UPDATE_THM] >> rw[]
QED


Theorem holds_LOWMOD[simp]:
  âˆ€p v. valuation (LOWMOD M) v â‡’
        (holds (LOWMOD M) v p â‡” holds M (term_of_num o v) p)
Proof
  Induct >> simp[Cong MAP_CONG, LOWMOD_termval]
  >- simp[LOWMOD_def, MAP_MAP_o, Cong MAP_CONG] >>
  rw[] >> simp[LOWMOD_def, PULL_EXISTS, term_of_num_composition]
QED

Theorem interpretation_LOWMOD[simp]:
  âˆ€L M. interpretation L (LOWMOD M) â‡” interpretation L M
Proof
  simp[interpretation_def, pairTheory.FORALL_PROD, LOWMOD_def] >>
  rw[EQ_IMP_THM]
  >- (rename [â€˜M.Fun f zs âˆˆ M.Domâ€™] >>
      first_x_assum (qspecl_then [â€˜fâ€™, â€˜MAP num_of_term zsâ€™] mp_tac) >>
      simp[MEM_MAP, PULL_EXISTS, MAP_MAP_o, Cong MAP_CONG]) >>
  first_x_assum irule >> simp[MEM_MAP, PULL_EXISTS] >> rpt strip_tac >>
  first_x_assum drule >> simp[PULL_EXISTS]
QED

Theorem LS:
  âˆ€M : Î± model s.
     interpretation (language s) M âˆ§ M.Dom â‰  âˆ… âˆ§ (âˆ€p. p âˆˆ s â‡’ qfree p) âˆ§
     M satisfies s
    â‡’
     âˆƒN: num model.
       interpretation (language s) N âˆ§ N.Dom â‰  âˆ… âˆ§ N satisfies s
Proof
  rw[] >> drule_all_then strip_assume_tac CANON >>
  qexists_tac â€˜LOWMOD Câ€™ >> simp[] >>
  fs[satisfies_def] >> rw[] >> first_x_assum irule >>
  fs[valuation_def, LOWMOD_def] >> metis_tac[TERM_OF_NUM]
QED

Theorem COMPACT_LS_ALPHA:
  INFINITE ð•Œ(:Î±) âˆ§
  (âˆ€t. FINITE t âˆ§ t âŠ† s â‡’
       âˆƒM:Î± model.
          interpretation (language s) M âˆ§ M.Dom â‰  âˆ… âˆ§ M satisfies t) â‡’
  âˆƒM:Î± model.
     interpretation (language s) M âˆ§ M.Dom â‰  âˆ… âˆ§ M satisfies s
Proof
  rw[] >> drule_then (qx_choose_then â€˜Cmâ€™ strip_assume_tac) COMPACT_LS >>
  â€˜âˆƒf. INJ f ð•Œ(:term) ð•Œ(:Î±)â€™
    by (fs[infinite_num_inj] >> qexists_tac â€˜f o num_of_termâ€™ >>
        fs[INJ_IFF]) >>
  metis_tac[copy_models]
QED

Theorem COMPACTNESS_satisfiable:
  INFINITE ð•Œ(:Î±) âˆ§ ffinsat (:Î±) s â‡’ satisfiable (:Î±) s
Proof
  rw[ffinsat_def, satisfiable_def] >>
  â€˜âˆ€t. FINITE t âˆ§ t âŠ† s â‡’
       âˆƒM. M.Dom â‰  âˆ… âˆ§ interpretation (language s) M âˆ§ M satisfies tâ€™
    by (rw[] >> first_x_assum (drule_all_then strip_assume_tac) >>
        metis_tac[interpretation_extendlanguage]) >>
  metis_tac[COMPACT_LS_ALPHA]
QED

Definition tm_constify_def[simp]:
  (tm_constify kvs (V x) = if x âˆˆ kvs then V x else Fn (1 âŠ— x) []) âˆ§
  (tm_constify kvs (Fn f zs) = Fn (0 âŠ— f) (MAP (tm_constify kvs) zs))
Termination
  WF_REL_TAC â€˜measure (term_size o SND)â€™ >> simp[]
End

Definition varify_map_def:
  varify_map kvs m k = if k âˆˆ kvs then V k else m k
End

Theorem tm_constify_varify:
  tm_constify keeps t =
    termsubst (varify_map keeps (Î»n. Fn (1 âŠ— n) [])) (bumpterm t)
Proof
  Induct_on â€˜tâ€™ >> simp[MAP_MAP_o, combinTheory.o_ABS_R, Cong MAP_CONG] >>
  simp[varify_map_def]
QED

Definition fm_constify_def[simp]:
  fm_constify kvs False = False âˆ§
  fm_constify kvs (Pred n zs) = Pred n (MAP (tm_constify kvs) zs) âˆ§
  fm_constify kvs (IMP p1 p2) = IMP (fm_constify kvs p1) (fm_constify kvs p2) âˆ§
  fm_constify kvs (FALL x p) = FALL x (fm_constify (x INSERT kvs) p)
End

Theorem FVT_tm_constify[simp]:
  âˆ€t. FVT (tm_constify kvs t) = kvs âˆ© FVT t
Proof
  Induct >> simp[MAP_MAP_o, combinTheory.o_ABS_R, Cong MAP_CONG]
  >- (rw[EXTENSION] >> csimp[]) >>
  simp[Once EXTENSION, MEM_MAP, PULL_EXISTS] >> metis_tac[]
QED

Theorem FV_fm_constify[simp]:
  âˆ€p kvs. FV(fm_constify kvs p) = kvs âˆ© FV p
Proof
  Induct >> simp[fm_constify_def, formsubst_FV, UNION_OVER_INTER]
  >- (simp[Once EXTENSION, MEM_MAP, PULL_EXISTS] >> metis_tac[]) >>
  simp[Once EXTENSION] >> csimp[] >> metis_tac[]
QED

Definition constifymod_def:
  constifymod v M = <| Dom := M.Dom ;
                       Fun := Î»g zs. if nfst g = 1 âˆ§ zs = [] then
                                       v (nsnd g)
                                     else M.Fun (nsnd g) zs ;
                       Pred := M.Pred |>
End

Theorem constifymod_rwts[simp]:
  (constifymod v M).Dom = M.Dom âˆ§ (constifymod v M).Pred = M.Pred
Proof
  simp[constifymod_def]
QED

Theorem termval_constify[simp]:
  termval (constifymod v M) w (tm_constify kvs t) =
  termval M (Î»vnm. if vnm âˆˆ kvs then w vnm else v vnm) t
Proof
  Induct_on â€˜tâ€™ >> simp[MAP_MAP_o, combinTheory.o_ABS_R, Cong MAP_CONG] >>
  simp[constifymod_def] >> rw[]
QED

Theorem holds_constify:
  âˆ€Ï• kvs v w M.
    holds (constifymod v M) w (fm_constify kvs Ï•) â‡”
    holds M (Î»vnm. if vnm âˆˆ kvs then w vnm else v vnm) Ï•
Proof
  Induct_on â€˜Ï•â€™ >> simp[MAP_MAP_o, combinTheory.o_DEF]
  >- asm_simp_tac (srw_ss() ++ boolSimps.ETA_ss) [] >>
  rpt gen_tac >> rpt (AP_TERM_TAC ORELSE AP_THM_TAC ORELSE ABS_TAC) >>
  simp[FUN_EQ_THM, combinTheory.APPLY_UPDATE_THM] >> rw[] >> fs[]
QED

val _ = overload_on ("FMC", â€œfm_constify âˆ…â€)
Theorem holds_constify_thm[simp]:
  holds (constifymod v M) w (fm_constify âˆ… Ï•) â‡” holds M v Ï•
Proof
  simp_tac (srw_ss() ++ boolSimps.ETA_ss) [holds_constify]
QED

Theorem FMC_Not:
  FMC (Not Ï•) = Not (FMC Ï•)
Proof
  simp[Not_def]
QED

Theorem language_NOT_INSERT:
  language (Not Ï• INSERT s) = language (Ï• INSERT s)
Proof
  rw[language_def, functions_def, predicates_def] >>
  simp[Once EXTENSION, PULL_EXISTS] >> dsimp[Not_def]
QED

Theorem term_functions_tm_constify:
  term_functions (tm_constify kvs t) =
    { (0 âŠ— n, l) | (n,l) âˆˆ term_functions t } âˆª
    { (1 âŠ— n, 0) | n âˆˆ FVT t âˆ§ n âˆ‰ kvs }
Proof
  Induct_on â€˜tâ€™ >> simp[]
  >- (rw[] >> csimp[] >> simp[EXTENSION]) >>
  simp[MEM_MAP, PULL_EXISTS, Cong MAP_CONG, MAP_MAP_o] >>
  dsimp[Once EXTENSION, MEM_MAP, PULL_EXISTS] >> metis_tac[]
QED

Theorem form_functions_fm_constify:
  âˆ€p kvs. form_functions (fm_constify kvs p) =
            { (0 âŠ— n, l) | (n,l) âˆˆ form_functions p } âˆª
            { (1 âŠ— n, 0) | n âˆˆ FV p âˆ§ n âˆ‰ kvs }
Proof
  Induct_on â€˜pâ€™ >>
  simp[MAP_MAP_o, MEM_MAP, combinTheory.o_DEF, PULL_EXISTS,
       term_functions_tm_constify]
  >- (dsimp[Once EXTENSION, MEM_MAP, PULL_EXISTS] >> metis_tac[])
  >- (simp[Once EXTENSION] >> metis_tac[]) >>
  simp[Once EXTENSION] >> metis_tac[]
QED

Theorem form_predicates_fm_constify[simp]:
  âˆ€p kvs. form_predicates (fm_constify kvs p) = form_predicates p
Proof
  Induct >> simp[]
QED

Theorem functions_fmc:
  functions {FMC p | p âˆˆ s} =
    { (0 âŠ— n,l) | (n,l) âˆˆ functions s } âˆª
    { (1 âŠ— n,0) | n âˆˆ BIGUNION (IMAGE FV s) }
Proof
  dsimp[functions_def, Once EXTENSION, PULL_EXISTS,
        form_functions_fm_constify] >> metis_tac[]
QED

Theorem predicates_fmc[simp]:
  predicates {FMC p | p âˆˆ s } = predicates s
Proof
  simp[predicates_def, Once EXTENSION, PULL_EXISTS]
QED

Theorem valuation_constifymod[simp]:
  valuation (constifymod v M) w â‡” valuation M w
Proof
  simp[valuation_def]
QED

Theorem constifymod_interpretation:
  valuation M v â‡’
    (interpretation (language {FMC p | p âˆˆ s}) (constifymod v M) â‡”
     interpretation (language s) M)
Proof
  dsimp[interpretation_def, language_def, functions_fmc, valuation_def] >>
  simp[constifymod_def] >> metis_tac[]
QED

Definition uncm_mod_def:
  uncm_mod M = <| Dom := M.Dom ;
                  Fun := Î»g zs. M.Fun (0 âŠ— g) zs ;
                  Pred := M.Pred |>
End

Theorem uncm_mod_rwts[simp]:
  (uncm_mod M).Dom = M.Dom âˆ§
  (uncm_mod M).Fun n l = M.Fun (0 âŠ— n) l âˆ§
  (uncm_mod M).Pred = M.Pred
Proof
  simp[uncm_mod_def]
QED

Definition uncm_map_def:
  uncm_map M fvs vnm = if vnm âˆˆ fvs then M.Fun (1 âŠ— vnm) []
                      else @a. a âˆˆ M.Dom
End

Theorem termval_uncm:
  âˆ€t. (FVT t DIFF kvs) âŠ† fs â‡’
      termval M v (tm_constify kvs t) =
      termval (uncm_mod M)
              (Î»vnm. if vnm âˆˆ kvs then v vnm else uncm_map M fs vnm)
              t
Proof
  Induct >> simp[] >- rw[uncm_map_def] >>
  strip_tac >> simp[MAP_MAP_o, combinTheory.o_ABS_R] >>
  â€˜âˆ€t. MEM t ts â‡’ FVT t DIFF kvs âŠ† fsâ€™
    by (fs[SUBSET_DEF, MEM_MAP, PULL_EXISTS] >> metis_tac[]) >>
  simp[Cong MAP_CONG]
QED

Theorem UNION_DIFF_lemma:
  (s âˆª t) DIFF r = (s DIFF r) âˆª (t DIFF r)
Proof
  simp[EXTENSION] >> metis_tac[]
QED

Theorem holds_uncm:
  âˆ€Ï• v kvs fs.
     FV Ï• DIFF kvs âŠ† fs â‡’
     (holds (uncm_mod M)
            (Î»vnm. if vnm âˆˆ kvs then v vnm else uncm_map M fs vnm)
            Ï• â‡” holds M v (fm_constify kvs Ï•))
Proof
  Induct_on â€˜Ï•â€™ >> simp[MAP_MAP_o, combinTheory.o_DEF, UNION_DIFF_lemma]
  >- (rpt strip_tac >> rename [â€˜M.Pred pnm (MAP _ zs)â€™] >>
      â€˜âˆ€t. MEM t zs â‡’ FVT t DIFF kvs âŠ† fsâ€™
        by (fs[SUBSET_DEF, MEM_MAP, PULL_EXISTS] >> metis_tac[]) >>
      asm_simp_tac (srw_ss() ++ boolSimps.ETA_ss)
        [GSYM termval_uncm, Cong MAP_CONG]) >>
  rpt strip_tac >>
  â€˜FV Ï• DIFF (n INSERT kvs) âŠ† fsâ€™ by fs[SUBSET_DEF] >>
  first_x_assum (drule_then (assume_tac o GSYM)) >> simp[] >>
  rpt (AP_TERM_TAC ORELSE AP_THM_TAC ORELSE ABS_TAC) >>
  simp[FUN_EQ_THM, combinTheory.APPLY_UPDATE_THM] >> rw[] >> fs[]
QED

Theorem interpretation_uncm:
  interpretation (language {FMC p | p âˆˆ s}) M â‡’
  interpretation (language s) (uncm_mod M)
Proof
  simp[interpretation_def, language_def, functions_fmc, PULL_EXISTS]
QED

Theorem interpretation_uncm_valuation:
  interpretation (language {FMC p | p âˆˆ s}) M âˆ§ M.Dom â‰  âˆ… â‡’
  valuation (uncm_mod M) (uncm_map M (BIGUNION (IMAGE FV s)))
Proof
  dsimp[interpretation_def, language_def, functions_fmc, PULL_EXISTS,
        valuation_def, uncm_map_def] >> rw[] >> fs[EXTENSION] >>
  SELECT_ELIM_TAC >> simp[] >> metis_tac[]
QED

Theorem entails_constify:
  entails (:Î±) {FMC p | p âˆˆ H} (FMC Ï•) â‡” entails (:Î±) H Ï•
Proof
  simp[entails_def, hold_def] >>
  qmatch_abbrev_tac â€˜P = Qâ€™ >> â€˜Â¬P â‡” Â¬Qâ€™ suffices_by simp[] >>
  simp_tac pure_ss [NOT_FORALL_THM, Abbrâ€˜Pâ€™, Abbrâ€˜Qâ€™,
                    DECIDE â€œÂ¬(p â‡’ q) â‡” p âˆ§ Â¬qâ€, PULL_EXISTS, GSYM HOLDS,
                    GSYM FMC_Not] >>
  ONCE_REWRITE_TAC[GSYM language_NOT_INSERT] >>
  ONCE_REWRITE_TAC[GSYM FMC_Not] >>
  qspec_tac(â€˜Not Ï•â€™, â€˜Ïˆâ€™) >> qx_gen_tac â€˜Ï•â€™ >> reverse eq_tac >> rw[]
  >- (map_every qexists_tac [â€˜constifymod v Mâ€™, â€˜vâ€™] >> simp[] >>
      qmatch_abbrev_tac â€˜interpretation (language FML) _â€™ >>
      â€˜FML = { FMC p | p âˆˆ Ï• INSERT H }â€™ by dsimp[EXTENSION, Abbrâ€˜FMLâ€™] >>
      pop_assum SUBST1_TAC >> simp[constifymod_interpretation]) >>
  qabbrev_tac â€˜A = Ï• INSERT Hâ€™ >>
  qabbrev_tac â€˜fvs = BIGUNION (IMAGE FV A)â€™ >>
  â€˜FV Ï• DIFF âˆ… âŠ† fvsâ€™ by simp[Abbrâ€˜Aâ€™, Abbrâ€˜fvsâ€™] >>
  â€˜âˆ€p. p âˆˆ H â‡’ FV p DIFF âˆ… âŠ† fvsâ€™
     by (fs[Abbrâ€˜Aâ€™, Abbrâ€˜fvsâ€™, SUBSET_DEF, PULL_EXISTS] >> metis_tac[]) >>
  FREEZE_THEN (fn th => fs[th])
    (holds_uncm |> SPEC_ALL |> Q.INST [â€˜fsâ€™ |-> â€˜fvsâ€™, â€˜kvsâ€™ |-> â€˜âˆ…â€™]
                |> SIMP_RULE (srw_ss()) [] |> GSYM |> Q.GENL [â€˜Ï•â€™]) >>
  qmatch_assum_abbrev_tac â€˜interpretation (language FML) _â€™ >>
  â€˜FML = {FMC p | p âˆˆ A}â€™
     by (simp[Abbrâ€˜FMLâ€™, Abbrâ€˜Aâ€™, EXTENSION] >> metis_tac[]) >>
  map_every qexists_tac [â€˜uncm_mod Mâ€™, â€˜uncm_map M fvsâ€™] >> simp[] >>
  full_simp_tac(srw_ss() ++ boolSimps.ETA_ss)[interpretation_uncm] >>
  Q.UNABBREV_TAC â€˜fvsâ€™ >> simp[interpretation_uncm_valuation]
QED

(* but not the reverse: there might be one model that makes all the new
   constants satisfy the constraints, but then when those constants turn
   back into variables, it's by no means guaranteed that every valuation
   will satisfy the constraints *)
Theorem satisfiable_constify:
  satisfiable (:Î±) s â‡’ satisfiable (:Î±) { FMC p | p âˆˆ s }
Proof
  simp[satisfiable_def, satisfies_def, PULL_EXISTS] >> rpt strip_tac >>
  â€˜âˆƒd. d âˆˆ M.Domâ€™ by (fs[EXTENSION] >> metis_tac[]) >>
  â€˜valuation M (K d : num -> Î±)â€™ by simp[valuation_def] >>
  qexists_tac â€˜constifymod (K d) Mâ€™ >>
  simp[constifymod_interpretation]
QED

Theorem holds_FMC_ARB:
  holds M v (FMC p) â‡” holds M ARB (FMC p)
Proof
  simp[holds_valuation]
QED


Theorem not_entails:
  Â¬entails (:Î±) H Ï• â‡” satisfiable (:Î±) { FMC p | p âˆˆ Not Ï• INSERT H }
Proof
  ONCE_REWRITE_TAC [GSYM entails_constify] >>
  â€˜âˆ€P s. {FMC p | P p} = IMAGE FMC {x | P x}â€™
    by (simp[EXTENSION] >> metis_tac[IN_DEF]) >>
  dsimp[entails_def, hold_def, satisfiable_def, satisfies_def, PULL_EXISTS,
        holds_FMC_ARB, FMC_Not, language_NOT_INSERT] >>
  eq_tac >> rw[] >- (qexists_tac â€˜Mâ€™ >> simp[]) >>
  qexists_tac â€˜Mâ€™ >> simp[] >>
  â€˜âˆƒv:num -> Î±. valuation M vâ€™ suffices_by metis_tac[] >>
  fs[valuation_def, EXTENSION] >> rename [â€˜d âˆˆ M.Domâ€™] >> qexists_tac â€˜K dâ€™ >>
  simp[]
QED

Theorem entails_mono:
  entails (:Î±) Î“â‚ Ï• âˆ§ Î“â‚ âŠ† Î“â‚‚ â‡’ entails (:Î±) Î“â‚‚ Ï•
Proof
  rw[entails_def, hold_def] >> last_x_assum irule >> simp[] >>
  â€˜interpretation (language (Ï• INSERT Î“â‚)) Mâ€™ suffices_by
    metis_tac[SUBSET_DEF] >>
  fs[language_def] >> irule interpretation_sublang >>
  goal_assum (first_assum o mp_then Any mp_tac) >>
  fs[SUBSET_DEF, functions_def] >> metis_tac[]
QED

Theorem satisfiable_antimono:
  satisfiable (:Î±) sâ‚ âˆ§ sâ‚‚ âŠ† sâ‚ â‡’ satisfiable (:Î±) sâ‚‚
Proof
  simp[satisfiable_def, SUBSET_DEF, satisfies_def] >> strip_tac >>
  â€˜interpretation (language sâ‚‚) Mâ€™ suffices_by metis_tac[] >>
  fs[language_def] >> irule interpretation_sublang >>
  goal_assum (first_assum o mp_then Any mp_tac) >>
  simp[functions_def, SUBSET_DEF, PULL_EXISTS] >> metis_tac[]
QED

Theorem tm_constify_11[simp]:
  âˆ€t1 t2. tm_constify ks t1 = tm_constify ks t2 â‡” t1 = t2
Proof
  Induct >> Cases_on â€˜t2â€™ >> simp[] >> rw[]
  >- metis_tac[]
  >- metis_tac[] >>
  rw[EQ_IMP_THM] >> fs[LIST_EQ_REWRITE, EL_MAP] >>
  rfs[EL_MAP] >> metis_tac[MEM_EL]
QED

Theorem fm_constify_11[simp]:
  âˆ€f1 f2 ks. fm_constify ks f1 = fm_constify ks f2 â‡” f1 = f2
Proof
  Induct >> Cases_on â€˜f2â€™ >> csimp[INJ_MAP_EQ_IFF, INJ_IFF]
QED

Theorem finite_entailment:
  INFINITE ð•Œ(:Î±) â‡’
    (entails (:Î±) Î“ Ï• â‡” âˆƒÎ“â‚€. FINITE Î“â‚€ âˆ§ Î“â‚€ âŠ† Î“ âˆ§ entails (:Î±) Î“â‚€ Ï•)
Proof
  strip_tac >> reverse eq_tac >- metis_tac[entails_mono] >> CCONTR_TAC >>
  fs[] >>
  pop_assum (fn th =>
    â€˜âˆ€Î“â‚€. FINITE Î“â‚€ âˆ§ Î“â‚€ âŠ† Î“ â‡’ Â¬entails (:Î±) Î“â‚€ Ï•â€™ by metis_tac[th]) >>
  fs[not_entails] >>
  â€˜ffinsat (:Î±) { FMC p | p âˆˆ (Not Ï•) INSERT Î“ }â€™
    suffices_by (
      strip_tac >> drule_all COMPACTNESS_satisfiable >>
      metis_tac[not_entails]
    ) >>
  simp[ffinsat_def] >> rpt strip_tac >>
  rename [â€˜FINITE tâ€™, â€˜t âŠ† _ â€™] >>
  â€˜satisfiable (:Î±) (FMC (Not Ï•) INSERT t)â€™
     suffices_by metis_tac[SUBSET_OF_INSERT, satisfiable_antimono] >>
  â€˜âˆƒt0. FMC (Not Ï•) INSERT t = {FMC p | p = Not Ï• âˆ¨ p âˆˆ t0} âˆ§ FINITE t0 âˆ§
        t0 âŠ† Î“â€™ suffices_by metis_tac[] >>
  qexists_tac â€˜PREIMAGE FMC t DELETE Not Ï•â€™ >>
  dsimp[EXTENSION] >> rw[]
  >- (rw[EQ_IMP_THM] >> fs[SUBSET_DEF] >> first_x_assum drule >>
      rw[] >> metis_tac[])
  >- (irule FINITE_PREIMAGE >> simp[]) >>
  fs[SUBSET_DEF] >> rw[] >> first_x_assum drule >> simp[]
QED

val _ = export_theory()
