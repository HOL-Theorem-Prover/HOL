(* See:
     Forster, Kunze and Roth,
     "The Weak Call-by-Value ðœ†-Calculus Is Reasonable for Both Time and Space", POPL 2020
   for inspiration
*)

(* Added assumptions for closed terms for some theorems (commented)
    due to the difference between
      how substitutions are defined
        in HOL library and in Forster etc.'s Coq proof *)

open HolKernel Parse boolLib bossLib;
open arithmeticTheory;
open listTheory combinTheory;

open PrelimsTheory;
open pure_dBTheory;

val _ = new_theory "weak_CBV";

(* ------------------
              CBV LC
   ------------------ *)

Definition size:
        size s =
                case s of
                        | dV x => 1 + x
                        | dABS s => 1 + (size s)
                        | dAPP s t => 1 + size s + size t
End

(* --------------------------------
          Substitution and Closedness
   -------------------------------- *)

Overload subst = â€œ\s k u. sub u k sâ€

Theorem size_eqs:
  size (dV x) = 1 + x âˆ§
  size (dABS s) = 1 + size s âˆ§
  size (dAPP s t) = 1 + size s + size t
Proof
  rw[] >> rw[Once size]
QED

Inductive bound:
        (âˆ€k n. n < k â‡’ bound k (dV n)) âˆ§
        (âˆ€k s t. bound k s âˆ§ bound k t â‡’ bound k (dAPP s t)) âˆ§
        (âˆ€k s. bound (SUC k) s â‡’ bound k (dABS s))
End

Definition closed:
        closed s = bound 0 s
End

Theorem lift_bound:
  âˆ€n s. bound n s â‡’ (lift s n = s)
Proof
  Induct_on `s` >>
  rw[lift_def]
  >- rw[Once bound_cases] >>
  pop_assum (strip_assume_tac o ONCE_REWRITE_RULE[bound_cases]) >>
  gvs[] >> metis_tac[ADD1]
QED

Theorem lift_closed:
  closed s â‡’ (lift s 0 = s)
Proof
  strip_tac >> match_mp_tac lift_bound >> fs[closed]
QED

Definition lambda:
        lambda s = âˆƒt. s = dABS t
End

Theorem lambda_lam:
        lambda (dABS s)
Proof
        rw[lambda]
QED

Theorem bound_closed_k:
        âˆ€s k u. bound k s â‡’ subst s k u = s
Proof
        Induct_on `s` >> rw[]
  >- fs[Once bound_cases]
  >- (pop_assum mp_tac >> rw[Once bound_cases])
  >- (pop_assum mp_tac >> rw[Once bound_cases])
  >> pop_assum mp_tac >> rw[Once bound_cases] >>
  fs[ADD1]
QED

Theorem bound_ge:
        âˆ€k s. bound k s â‡’ âˆ€m. k â‰¤ m â‡’ bound m s
Proof
        Induct_on `s` >> rw[]
        >- fs[Once bound_cases]
        >- (qpat_x_assum (`bound _ _`) mp_tac >>
                rw[Once bound_cases] >>
                first_x_assum drule_all >> rw[] >>
                first_x_assum drule_all >> rw[] >>
                rw[Once bound_cases])
        >> qpat_x_assum (`bound _ _`) mp_tac >>
        rw[Once bound_cases] >>
        first_x_assum drule >> rw[] >>
        rw[Once bound_cases]
QED

Theorem bound_closed:
        âˆ€s k u. bound 0 s â‡’ subst s k u = s
Proof
  rw[] >> drule bound_ge >> rw[] >>
  metis_tac[bound_closed_k]
QED

(* Took ``Forall u`` out of the parenthesis and moved it up to the outter most level.
   Added ``closed u`` as an extra assumption *)
Theorem closed_k_bound:
        âˆ€k s u.
    closed u â‡’
    (âˆ€n. k â‰¤ n â‡’ subst s n u = s) â‡’
    bound k s
Proof
        Induct_on `s` >> rw[]
  >- (Cases_on `k â‰¤ n` >> rw[]
      >- (first_x_assum drule  >> rw[] >>
          fs[closed, Once bound_cases])
      >> fs[NOT_LEQ] >> rw[Once bound_cases])
  >- (last_x_assum drule >> rw[] >>
      last_x_assum drule >> rw[] >>
      rw[Once bound_cases])
        >> rw[Once bound_cases, ADD1] >>
  first_x_assum irule >> rw[] >>
  qexists_tac `u` >> rw[] >>
  drule lift_closed >> rw[] >>
  fs[] >>
  first_x_assum (qspec_then `n - 1` mp_tac) >> rw[]
QED

(* Took ``Forall t`` out of the parenthesis and moved it up to the outter most level.
   Added ``closed t`` on both sides of the theorem *)
Theorem closed_subst_iff:
  âˆ€s t. closed s âˆ§ closed t â‡” closed t âˆ§ (âˆ€k. subst s k t = s)
Proof
  rw[EQ_IMP_THM, closed]
  >- metis_tac[bound_closed]
  >> metis_tac[closed_k_bound, closed]
QED

(* Took ``Forall t`` out of the parenthesis and moved it up to the outter most level.
   Added ``closed t`` on the LHS of the theorem *)
Theorem closed_subst:
  âˆ€s t. closed s âˆ§ closed t â‡’ (âˆ€k. subst s k t = s)
Proof
  metis_tac[closed_subst_iff]
QED

Theorem closed_app:
  âˆ€s t. closed (dAPP s t) â‡’ closed s âˆ§ closed t
Proof
  rw[closed, Once bound_cases]
QED

Theorem app_closed:
  âˆ€s t. closed s â‡’ closed t â‡’ closed (dAPP s t)
Proof
  rw[closed] >> rw[Once bound_cases]
QED

(* Added ``closed t`` *)
Theorem bound_subst:
  âˆ€s t k.
    closed t â‡’
    bound (SUC k) s â‡’
    bound k t â‡’
    bound k (subst s k t)
Proof
  Induct_on `s` >> rw[]
  >- fs[Once bound_cases]
  >- (rw[Once bound_cases] >>
      qpat_x_assum `bound (SUC _) _` mp_tac >>
      rw[Once bound_cases])
  >> rw[Once bound_cases] >>
  qpat_x_assum `bound (SUC _) _` mp_tac >>
  rw[Once bound_cases] >>
  drule lift_closed >> rw[] >>
  first_x_assum drule >> rw[] >>
  first_x_assum drule >> rw[] >>
  `bound (SUC k) t` by (irule bound_ge >> qexists_tac `k` >> rw[]) >>
  metis_tac[ADD1]
QED

Theorem closed_subst2:
  âˆ€s t. closed (dABS s) â‡’ closed t â‡’ closed (subst s 0 t)
Proof
  rw[closed] >>
  irule bound_subst >> rw[closed] >>
  qpat_x_assum `bound _ (dABS _)` mp_tac >> rw[Once bound_cases]
QED

(* ----------------------------
          Deterministic Reduction
   ---------------------------- *)

Inductive step:
[~App:]
        (âˆ€s t. step (dAPP (dABS s) (dABS t)) (subst s 0 (dABS t)))
[~AppR:]
        (âˆ€s t t'. step t t' â‡’ step (dAPP (dABS s) t) (dAPP (dABS s) t'))
[~AppL:]
        (âˆ€s s' t. step s s' â‡’ step (dAPP s t) (dAPP s' t))
End

(* -----------------------
           Resource Measures
   ----------------------- *)

(* -- Small-Step Time Measure -- *)

Theorem NRC_step_congL:
  W (respectful (NRC step k) (respectful ($=) (NRC step k))) dAPP
Proof
  Induct_on `k` >> rw[]
  >- rw[respectful, NRC]
  >> fs[respectful, NRC] >> rw[] >>
  qexists_tac `dAPP z y'` >> rw[] >>
  rw[Once step_cases]
QED

Theorem NRC_step_congR:
  W (respectful ($=) (respectful (NRC step k) (NRC step k))) (Î»s t. dAPP (dABS s) t)
Proof
  Induct_on `k` >> rw[]
  >- rw[respectful]
  >> fs[respectful] >> rw[] >>
  fs[Once NRC] >>
  first_x_assum drule >> rw[] >>
  first_x_assum (qspecl_then [`y`] ASSUME_TAC) >>
  qexists_tac `(dAPP (dABS y) z)` >> rw[] >>
  rw[Once step_cases]
QED

(* -- Small-Step Space Measure -- *)

Definition redWithMaxSizeL:
        redWithMaxSizeL = redWithMaxSize size step
End

Theorem redWithMaxSizeL_congL:
  âˆ€m m' s s' t.
    redWithMaxSizeL m s s' â‡’
    m' = 1 + m + size t â‡’
    redWithMaxSizeL m' (dAPP s t) (dAPP s' t)
Proof
  simp[redWithMaxSizeL] >>
  Induct_on `redWithMaxSize` >> rw[]
  >- (irule redWithMaxSize_R >>
      `size (dAPP s t) = size s + (size t + 1)` suffices_by simp[] >>
      rw[Once size])
  >> irule redWithMaxSize_C >>
  qexistsl_tac [`(m' + (size t + 1))`, `dAPP s' t`] >> rw[]
  >- rw[Once step_cases]
  >> `MAX (size (dAPP s t)) (m' + (size t + 1))
      = size t + (MAX (size s) m' + 1)` suffices_by simp[] >>
  rw[Once size] >> rw[MAX_DEF]
QED

Theorem redWithMaxSizeL_redWithMaxSize_congL:
  âˆ€m m' s s' t.
    redWithMaxSize size step m s s' â‡’
    m' = 1 + m + size t â‡’
    redWithMaxSize size step m' (dAPP s t) (dAPP s' t)
Proof
  metis_tac[redWithMaxSizeL_congL, redWithMaxSizeL]
QED

Theorem redWithMaxSizeL_congR:
  âˆ€m m' s t t'.
    redWithMaxSizeL m t t' â‡’
    m' = (1 + m + size (dABS s)) â‡’
    redWithMaxSizeL m' (dAPP (dABS s) t) (dAPP (dABS s) t')
Proof
  simp[redWithMaxSizeL] >>
  Induct_on `redWithMaxSize` >> rw[]
  >- (irule redWithMaxSize_R >>
      `size (dAPP (dABS s) t) = size t + (size (dABS s) + 1)` suffices_by simp[] >>
      rw[Once size])
  >> irule redWithMaxSize_C >>
  qexistsl_tac [`(m' + (size (dABS s) + 1))`, `(dAPP (dABS s) t')`] >> rw[]
  >- rw[Once step_cases]
  >> `MAX (size (dAPP (dABS s) t)) (m' + (size (dABS s) + 1))
      = size (dABS s) + (MAX (size t) m' + 1)` suffices_by simp[] >>
  rw[Once size] >> rw[MAX_DEF]
QED

Theorem redWithMaxSizeL_redWithMaxSize_congR:
  âˆ€m m' s t t'.
    redWithMaxSize size step m t t' â‡’
    m' = (1 + m + size (dABS s)) â‡’
    redWithMaxSize size step m' (dAPP (dABS s) t) (dAPP (dABS s) t')
Proof
  metis_tac[redWithMaxSizeL_congR, redWithMaxSizeL]
QED

(* -- Big-Step Time Measure -- *)

Inductive timeBS:
[~Val:]
  (âˆ€s. timeBS (0:num) (dABS s) (dABS s))
[~Beta:]
  (âˆ€s s' t t' u i j k l.
    timeBS i s (dABS s') âˆ§
    timeBS j t (dABS t') âˆ§
    timeBS k (subst s' 0 (dABS t')) u âˆ§
    l = i+j+1+k â‡’
    timeBS l (dAPP s t) u)
End

Theorem closed_timeBS:
  âˆ€k s t.
    closed s â‡’
    timeBS k s t â‡’
    closed t
Proof
  Induct_on `timeBS` >> rw[] >>
  first_x_assum irule >>
  drule closed_app >> rw[] >>
  first_x_assum drule >> rw[] >>
  first_x_assum drule >> rw[] >>
  metis_tac[closed_subst2]
QED

Theorem step_evaluatesIn:
  âˆ€s s' t k.
    step s s' â‡’
    timeBS k s' t â‡’
    timeBS (SUC k) s t
Proof
  Induct_on `step` >> rw[]
  >- (rw[Once timeBS_cases] >>
      irule_at (Pos hd) timeBS_Val >>
      irule_at (Pos hd) timeBS_Val >>
      rw[ADD1])
  >- (rw[Once timeBS_cases] >>
      pop_assum mp_tac >> rw[Once timeBS_cases] >>
      goal_assum drule >>
      first_x_assum drule >> rw[] >>
      goal_assum drule >>
      goal_assum drule >>
      rw[])
  >> rw[Once timeBS_cases] >>
  pop_assum mp_tac >> rw[Once timeBS_cases] >>
  first_x_assum drule >> rw[] >>
  goal_assum drule >>
  goal_assum drule >>
  goal_assum drule >>
  rw[]
QED

Theorem timeBS_correct:
  âˆ€s t k. timeBS k s t â‡” NRC step k s t âˆ§ lambda t
Proof
  rw[lambda] >> EQ_TAC
  >- (Induct_on `timeBS` >> rw[] >>
      (* s s'' ---<k>---> (\s') s'' *)
      irule NRC_ADD_EQN_R >>
      qexists_tac `dAPP (dABS s') s''` >> rw[]
      >- (`W (respectful (NRC step k) (respectful ($=) (NRC step k))) dAPP`
            by metis_tac[NRC_step_congL] >> fs[respectful]) >>
      (* (\s') s'' ---<k'>---> (\s') (\t') *)
      irule NRC_ADD_EQN_R >>
      qexists_tac `(dAPP (dABS s') (dABS t'))` >> rw[]
      >- (`W (respectful ($=) (respectful (NRC step k') (NRC step k')))
           (Î»s t. dAPP (dABS s) t)`
            by metis_tac[NRC_step_congR] >>
          fs[respectful]) >>
      (* (\s') (\t') ---<1>---> subst ... *)
      `NRC step (SUC k'') (dAPP (dABS s') (dABS t')) (dABS t'')`
        suffices_by simp[ADD1] >>
      rw[NRC] >> qexists_tac `subst s' 0 (dABS t')` >>
      rw[] >> rw[Once step_cases])
  >> MAP_EVERY qid_spec_tac [`k`, `s`, `t`] >>
  Induct_on `k` >> rw[]
  >- fs[Once timeBS_cases, NRC, Once step_cases]
  >> irule step_evaluatesIn >>
  pop_assum mp_tac >> rw[Once NRC] >>
  qexists_tac `z` >> rw[]
QED

(* -- Big-Step Space Measure -- *)

Inductive spaceBS:
[~Val:]
  (âˆ€s. spaceBS (size (dABS s)) (dABS s) (dABS s))
[~Beta:]
  (âˆ€s s' t t' u m1 m2 m3 m.
    spaceBS m1 s (dABS s') âˆ§
    spaceBS m2 t (dABS t')  âˆ§
    spaceBS m3 (subst s' 0 (dABS t')) u âˆ§
    m = MAX (m1 + 1 + size t)
            (MAX (size (dABS s') + 1 + m2) m3) â‡’
    spaceBS m (dAPP s t) u)
End

Theorem closed_spaceBS:
  âˆ€k s t.
    closed s â‡’
    spaceBS k s t â‡’
    closed t
Proof
  Induct_on `spaceBS` >> rw[] >>
  first_x_assum irule >>
  drule closed_app >> rw[] >>
  first_x_assum drule >> rw[] >>
  first_x_assum drule >> rw[] >>
  metis_tac[closed_subst2]
QED

Theorem spaceBS_ge:
  âˆ€s t m.
    spaceBS m s t â‡’ size s â‰¤ m âˆ§ size t â‰¤ m
Proof
  Induct_on `spaceBS` >> rw[] >>
  rw[Once size]
QED

Theorem step_evaluatesSpace:
  âˆ€s s' t m.
    step s s' â‡’
    spaceBS m s' t â‡’
    spaceBS (MAX (size s) m) s t
Proof
  Induct_on `step` >> rw[]
  >- (rw[Once spaceBS_cases] >>
      irule_at (Pos hd) spaceBS_Val >>
      irule_at (Pos hd) spaceBS_Val >>
      goal_assum drule >> rw[MAX_DEF]
      >- (fs[NOT_LESS] >> fs[Once size] >>
          qpat_x_assum `size _ + _ < _` mp_tac >>
          rw[Once size])
      >- (fs[NOT_LESS] >> fs[Once size] >>
          qpat_x_assum `m â‰¤ _` mp_tac >>
          rw[Once size])
      >> fs[NOT_LESS] >> fs[Once size])
  >- (rw[Once spaceBS_cases] >>
      pop_assum mp_tac >> rw[Once spaceBS_cases] >>
      goal_assum drule >>
      first_x_assum drule >> rw[] >>
      goal_assum drule >>
      goal_assum drule >>
      qpat_x_assum `spaceBS _ (dABS _) (dABS _)` mp_tac >>
      rw[Once spaceBS_cases] >>
      `size s'' â‰¤ m2 âˆ§ size (dABS t'') â‰¤ m2`
        by metis_tac[spaceBS_ge] >> rw[] >>
      `size (subst s 0 (dABS t'')) â‰¤ m3 âˆ§ size t â‰¤ m3`
        by metis_tac[spaceBS_ge] >> rw[] >>
      `size s' â‰¤ (MAX (size s') m2) âˆ§ size (dABS t'') â‰¤ (MAX (size s') m2)`
        by metis_tac[spaceBS_ge] >> rw[] >>
      `size (dABS s) = 1 + size s`
        by rw[Once size] >> rw[] >>
      `size (dAPP (dABS s) s') = 1 + (1 + size s) + size s'`
        by rw[Once size] >> rw[] >>
      `1 + size t'' â‰¤ MAX (size s') m2`
        by fs[Once size] >>
      pop_assum mp_tac >>
      rw[MAX_DEF])
  >> rw[Once spaceBS_cases] >>
  pop_assum mp_tac >> rw[Once spaceBS_cases] >>
  rename [`spaceBS m1 s' (dABS s'')`,
          `spaceBS m2 t (dABS t')`,
          `spaceBS m3 (subst s'' 0 (dABS t')) u`] >>
  first_x_assum drule >> rw[] >>
  goal_assum drule >>
  goal_assum drule >>
  goal_assum drule >>
  rw[Once size] >>
  `size (dABS s'') = 1 + size s''`
    by rw[Once size] >> rw[] >>
  first_x_assum (qspecl_then [] (K all_tac)) >>
  rw[MAX_ASSOC] >> rw[MAX_DEF]
QED

Theorem spaceBS_correctL:
  âˆ€s t m.
    spaceBS m s t â‡’
      redWithMaxSizeL m s t âˆ§ lambda t
Proof
  Induct_on `spaceBS` >> rw[]
  >- rw[redWithMaxSizeL, Once redWithMaxSize_cases]
  >- rw[lambda]
  >> fs[redWithMaxSizeL] >>
  irule redWithMaxSize_trans >>
  irule_at Any redWithMaxSizeL_redWithMaxSize_congL >> rw[] >>
  goal_assum drule >>
  (* irule_at (Pos hd) EQ_REFL >> *)
  irule_at Any redWithMaxSize_trans >>
  irule_at Any redWithMaxSizeL_redWithMaxSize_congR >> rw[] >>
  goal_assum drule >>
  (* irule_at (Pos hd) EQ_REFL >> *)
  rw[Once redWithMaxSize_cases] >>
  rw[RIGHT_AND_OVER_OR, EXISTS_OR_THM] >>
  DISJ2_TAC >> rw[PULL_EXISTS] >>
  goal_assum $ drule_at Any >> rw[]
  >- rw[Once step_cases] >>
  `size (dABS t') â‰¤ m'` by metis_tac[spaceBS_ge] >>
  fs[size_eqs] >> intLib.COOPER_TAC
QED

Theorem spaceBS_correctR:
  âˆ€s t m.
    redWithMaxSizeL m s t âˆ§ lambda t â‡’
      spaceBS m s t
Proof
  simp[redWithMaxSizeL] >>
  Induct_on `redWithMaxSize` >> rw[]
  >- fs[lambda, Once spaceBS_cases]
  >> irule step_evaluatesSpace >>
  first_x_assum drule >> metis_tac[]
QED

Theorem spaceBS_correct:
  âˆ€s t m.
    spaceBS m s t â‡”
      redWithMaxSizeL m s t âˆ§ lambda t
Proof
  metis_tac[spaceBS_correctL, spaceBS_correctR]
QED

val _ = export_theory ()
