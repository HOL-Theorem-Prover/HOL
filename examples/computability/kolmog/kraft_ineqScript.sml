
open HolKernel Parse boolLib bossLib finite_mapTheory;
open recursivefnsTheory;
open prnlistTheory;
open primrecfnsTheory;
open listTheory;
open arithmeticTheory;
open numpairTheory;
open pred_setTheory;
open recfunsTheory;
open extNatTheory;
open prtermTheory;
open pred_setTheory;
open extrealTheory;
open realTheory;
open real_sigmaTheory;
open lebesgueTheory;
open transcTheory;
open boolListsTheory;

local open util_probTheory in end

val _ = new_theory "kraft_ineq";

val _ = intLib.deprecate_int()




Definition len_fun_def:
  len_fun (A:bool list set) n =
  let strs = {s | (LENGTH s = n) /\ s IN A}
  in
    EXTREAL_SUM_IMAGE (\s. Normal (2 rpow (- &(LENGTH s)))) strs
End

Theorem extreal_sum_image_finite_corr = extrealTheory.EXTREAL_SUM_IMAGE_FINITE_CONST;

Theorem bool_list_card[simp]:
  CARD {s | LENGTH (s:bool list) = n} = 2**n
Proof
  Induct_on‚Äòn‚Äô>>simp[LENGTH_CONS] >> qmatch_abbrev_tac‚ÄòCARD s = 2**(SUC n)‚Äô>>
  ‚Äòs = IMAGE (CONS T) {s | LENGTH s = n} ‚à™ IMAGE (CONS F) {s | LENGTH s = n}‚Äô
    by (simp[Abbr‚Äòs‚Äô,EXTENSION,EQ_IMP_THM] >> rw[] ) >>
  simp[CARD_UNION_EQN,CARD_INJ_IMAGE,EXP] >>
  qmatch_abbrev_tac‚Äò(x:num)-y=x‚Äô >>
  ‚Äòy=0‚Äô suffices_by simp[] >> simp[Abbr‚Äòy‚Äô,EXTENSION] >>Cases>>simp[]>>
  metis_tac[]
QED

Theorem len_fun_eq1:
  SIGMA (\s. Normal (2 rpow -&LENGTH s)) {s | (LENGTH (s:bool list) = n)} = 1
Proof
  irule EQ_TRANS >>
  qexists_tac‚Äò&CARD {s | LENGTH (s:bool list) = n} * Normal(2 rpow (-&n))‚Äô >>
  conj_tac
  >- (irule extreal_sum_image_finite_corr >> rw[])
  >- (simp[extreal_of_num_def,extreal_mul_def,GSYM realTheory.REAL_OF_NUM_POW,
           GEN_RPOW, GSYM RPOW_ADD,RPOW_0 ] )
QED

Theorem len_fun_le1:
  len_fun A n <= 1
Proof
  rw[len_fun_def] >>
  ‚Äò{s | LENGTH s = n /\ s IN A} SUBSET {s|LENGTH s = n}‚Äô by simp[SUBSET_DEF] >>
  ‚ÄòFINITE {s | LENGTH s = n /\ s IN A}‚Äô
    by metis_tac[SUBSET_FINITE_I,finite_bool_list_n] >>
  ASSUME_TAC len_fun_eq1 >> pop_assum(SUBST1_TAC o SYM)>>
  irule EXTREAL_SUM_IMAGE_MONO_SET >>
  simp[finite_bool_list_n] >> rw[extreal_of_num_def,extreal_le_def] >>
  ‚Äò0r < 2 ‚Äôsuffices_by(strip_tac >> drule RPOW_POS_LT >>
                       simp[realTheory.REAL_LE_LT]) >>
  simp[]
QED

(* Traditional bool list to num *)
Theorem rev_fpow[simp]: REVERSE (Fpow n) = Fpow n
Proof simp[Fpow_def,REVERSE_GENLIST,combinTheory.K_DEF]
QED

Theorem rev_tpow[simp]: REVERSE (Tpow n) = Tpow n
Proof simp[Tpow_def,REVERSE_GENLIST,combinTheory.K_DEF]
QED

(* binary numbers in little-endian format *)
Definition tbl2n_def[simp]:
  tbl2n [] = 0n /\
  tbl2n (T::t) = 2*tbl2n t + 1 /\
  tbl2n (F::t) = 2*tbl2n t
End

(* binary numbers in big-endian format *)
Overload TBL2N = ‚Äú\l. tbl2n (REVERSE l)‚Äù

Definition inv_tbl2n_def:
  inv_tbl2n 0n = [] /\
  inv_tbl2n a = if EVEN a then [F]++(inv_tbl2n (a DIV 2))
                else [T]++(inv_tbl2n ((a-1) DIV 2 ))
Termination
  WF_REL_TAC‚Äò$<‚Äô >> rw[]>>
  irule LESS_EQ_LESS_TRANS >> qexists_tac‚Äòv‚Äô >> ‚Äò0<2n‚Äô by simp[] >>
  rw[DIV_LE_MONOTONE,DIV_LESS,DIV_LESS_EQ]
End

Theorem tbl2n_inv_tbl2n[simp]:
  tbl2n (inv_tbl2n n) = n
Proof
  completeInduct_on ‚Äòn‚Äô >> Cases_on‚Äòn‚Äô >> simp[tbl2n_def,inv_tbl2n_def] >>
  Cases_on‚ÄòEVEN (SUC n')‚Äô >>
  simp[tbl2n_def]
  >- (‚Äò2 * (SUC n' DIV 2) = (SUC n' DIV 2)*2‚Äô by simp[MULT_COMM] >>
      ‚Äò0<2n‚Äô by simp[] >>
      ‚ÄòSUC n' MOD 2=0‚Äô by metis_tac[EVEN_MOD2] >>
      ‚ÄòSUC n' DIV 2 * 2 + SUC n' MOD 2 = SUC n'‚Äô by metis_tac[GSYM DIVISION] >>
      fs[])
  >- (‚Äò0<2n‚Äô by simp[] >> ‚Äòn' DIV 2 <= n'‚Äô by simp[DIV_LESS_EQ] >>
      ‚Äòn' DIV 2 < SUC n'‚Äô by
        simp[LESS_EQ_IMP_LESS_SUC] >> fs[] >>
      ‚ÄòEVEN n'‚Äô by metis_tac[ODD,EVEN_OR_ODD] >>
      ‚Äò2 * (n' DIV 2) =  (n' DIV 2)*2‚Äô by simp[MULT_COMM] >> ‚Äò0<2n‚Äô by simp[] >>
      ‚Äòn' MOD 2=0‚Äô by metis_tac[EVEN_MOD2] >>
      ‚Äòn' DIV 2 * 2 + n' MOD 2 = n'‚Äô by metis_tac[GSYM DIVISION] >> fs[] )
QED

Definition interval_bl_def:
  interval_bl l = (&(TBL2N l) * (2 rpow -&LENGTH l), 2 rpow -&LENGTH l)
End

Definition disjoint_interval_def:
  disjoint_interval ((m:real),i) (n,j) <=>
  DISJOINT {r|m<=r /\ r<m+i} {r|n<=r /\ r<n+j}
End

Theorem tbl2n_exp2: tbl2n i * 2 ** n = tbl2n (Fpow n ++ i)
Proof
  rw[Fpow_def] >> Induct_on‚Äòn‚Äô >> simp[tbl2n_def,EXP,GENLIST_CONS]
QED

Theorem TBL2N_exp2: TBL2N i * 2 ** n = TBL2N (i ++ Fpow n)
Proof
  simp[REVERSE_APPEND] >> rw[Fpow_def] >> Induct_on‚Äòn‚Äô >>
  simp[tbl2n_def,EXP,GENLIST_CONS]
QED

Theorem tbl2n_lead0s[simp]: tbl2n (x ++ Fpow n) = tbl2n x
Proof
  Induct_on‚Äòx‚Äô >> simp[tbl2n_def]
  >- (‚Äòtbl2n (Fpow n ++ []) = tbl2n (Fpow n )‚Äô by simp[] >>
      ‚Äòtbl2n (Fpow n ++ []) = (tbl2n []) *2**n‚Äô by metis_tac[GSYM tbl2n_exp2] >>
      fs[tbl2n_def])
  >- (Cases_on ‚Äòh‚Äô >> simp[tbl2n_def])
QED

Theorem tbl2n_fpow[simp]: tbl2n (Fpow n) = 0
Proof simp[Fpow_def] >> Induct_on‚Äòn‚Äô >> simp[GENLIST_CONS]
QED

Theorem len_fpow[simp]: LENGTH (Fpow n) = n
Proof simp[Fpow_def]
QED

Theorem TBL2N_lead0s[simp]: TBL2N (Fpow n ++ x) = TBL2N x
Proof simp[REVERSE_APPEND]
QED

Theorem tbl2n_append_1:
  tbl2n (x++[e]) = if e then 2**(LENGTH x) + (tbl2n x) else tbl2n x
Proof
  Induct_on‚Äòx‚Äô >> simp[] >- (Cases_on‚Äòe‚Äô >> simp[]) >> Cases_on‚Äòh‚Äô >> simp[] >>
  rw[] >> simp[EXP]
QED

Theorem tbl2n_append: !y. tbl2n (x++y) = tbl2n x + 2**(LENGTH x) * tbl2n y
Proof
  Induct_on‚Äòx‚Äô >> simp[] >> Cases_on‚Äòh‚Äô >> simp[EXP]
QED

Theorem TBL2N_append:
  TBL2N (p++s) = 2**LENGTH s * (TBL2N p) +TBL2N s
Proof simp[REVERSE_APPEND,tbl2n_append]
QED

Theorem tbl2n_ub[simp]: tbl2n l < 2**(LENGTH l)
Proof Induct_on‚Äòl‚Äô >> simp[] >> Cases_on‚Äòh‚Äô >> simp[EXP]
QED

Theorem TBL2N_ub[simp]: TBL2N l < 2**(LENGTH l)
Proof metis_tac[tbl2n_ub,LENGTH_REVERSE]
QED

Theorem TBL2N_inj_len:
  !x y.(LENGTH x = LENGTH y) ==> (TBL2N x = TBL2N y <=> x=y)
Proof
  Induct_on‚Äòx‚Äô >> simp[] >> Cases_on‚Äòy‚Äô >> simp[] >> rw[tbl2n_append_1] >>
  ‚ÄòTBL2N t < 2** LENGTH t‚Äô by  (metis_tac[tbl2n_ub,LENGTH_REVERSE]) >>
  ‚ÄòTBL2N x < 2** LENGTH t‚Äô by  (metis_tac[tbl2n_ub,LENGTH_REVERSE]) >>simp[]
QED

Theorem TBL2N_lem1:
  !d. d<2**D ==>
      ?l. TBL2N l = d /\ LENGTH l = D /\
          TBL2N (i++Fpow D) +d = TBL2N (i++l)
Proof
  Induct_on‚ÄòD‚Äô >- simp[Fpow_def] >> rw[EXP] >> Cases_on‚Äòd<2**D‚Äô >> fs[]
  >- (res_tac >> qexists_tac‚ÄòF::l‚Äô >> simp[] >> fs[REVERSE_APPEND] >>
      simp[tbl2n_append])
  >- (qabbrev_tac‚Äòd0=d-2**D‚Äô >> ‚Äòd0 < 2**D‚Äô by simp[Abbr‚Äòd0‚Äô] >> res_tac >>
      qexists_tac‚ÄòT::l‚Äô >>
      simp[] >>fs[REVERSE_APPEND] >> simp[tbl2n_append] >> simp[Abbr‚Äòd0‚Äô] )
QED

Theorem disjoint_pf_lem1:
  TBL2N i * 2 ** (LENGTH j - LENGTH i) <= TBL2N j /\
  TBL2N j < (TBL2N i + 1) * 2 ** (LENGTH j - LENGTH i) /\
  LENGTH i < LENGTH j ==>
  i ‚â∫ j
Proof
  rw[TBL2N_exp2] >> qabbrev_tac‚ÄòD=LENGTH j ‚àí LENGTH i‚Äô >>
  ‚ÄòTBL2N (i ++ Fpow D) + (TBL2N j-TBL2N (i ++ Fpow D)) = TBL2N j‚Äô by simp[] >>
  qabbrev_tac‚Äòd=TBL2N j-TBL2N (i ++ Fpow D)‚Äô >>
  ‚Äòd<2**D‚Äô
    by (fs[RIGHT_ADD_DISTRIB] >>
        ‚ÄòTBL2N j - TBL2N i * 2 ** D < 2 ** D‚Äô by simp[] >>
        ‚ÄòTBL2N j ‚àí TBL2N (i ++ Fpow D) < 2**D‚Äô by simp[GSYM TBL2N_exp2] >>
        fs[Abbr‚Äòd‚Äô] ) >>
  ‚Äò?l. TBL2N l = d /\ LENGTH l = D /\ TBL2N (i++Fpow D) +d = TBL2N (i++l)‚Äô
    by simp[TBL2N_lem1] >>
  ‚ÄòTBL2N (i ++ l) = TBL2N j‚Äô by simp[] >>
  ‚ÄòLENGTH (i++l) = LENGTH j‚Äô by simp[Abbr‚ÄòD‚Äô] >>
  ‚Äòi++l = j‚Äô by metis_tac[TBL2N_inj_len] >> ‚Äò0<LENGTH l‚Äô by simp[Abbr‚ÄòD‚Äô] >>
  metis_tac[prefix_lem1]
QED

Theorem powr_negexp:
  0 < a ‚áí (a:real) powr -&x = inv (a pow x)
Proof
  ‚Äò-&x = 0r - &x‚Äô by simp[] >> pop_assum SUBST1_TAC >>
  simp[RPOW_SUB, RPOW_0, GSYM REAL_INV_1OVER, GSYM GEN_RPOW]
QED

Theorem disjoint_prefix_free:
  prefix_free P <=>
  let is = IMAGE interval_bl P
  in
    !i1 i2. i1 IN is /\ i2 IN is /\ i1<>i2 ==> disjoint_interval i1 i2
Proof
  rw[EQ_IMP_THM]
  >- (rename[‚Äòinterval_bl i = interval_bl j‚Äô] >>
      ‚Äò~(i ‚â∫ j) /\ ~(j ‚â∫ i)/\ i<>j‚Äô by metis_tac[prefix_free_def] >>
      fs[interval_bl_def,disjoint_interval_def,DISJOINT_DEF,EXTENSION,
         churchDBTheory.DISJ_IMP_EQ] >>
      rw[GSYM DISJ_ASSOC] >> rw[DECIDE‚Äú~p\/q <=> p==>q‚Äù] >> strip_tac >>
      ‚ÄòLENGTH i <> LENGTH j‚Äô
        by (strip_tac >> fs[] >> qabbrev_tac‚ÄòM = 2 rpow -&LENGTH j‚Äô >>
            ‚Äò&TBL2N i * M < &TBL2N j * M + M‚Äô by metis_tac[REAL_LET_TRANS] >>
            ‚Äò&TBL2N j * M < &TBL2N i * M + M‚Äô by metis_tac[REAL_LET_TRANS] >>
            ‚Äò&TBL2N i * M + M = (&TBL2N i + 1) * M‚Äô
              by metis_tac[REAL_ADD_RDISTRIB,REAL_MUL_LID] >>
            ‚Äò&TBL2N j * M + M = (&TBL2N j + 1) * M‚Äô
              by metis_tac[REAL_ADD_RDISTRIB,REAL_MUL_LID] >> fs[] >>
            ‚Äò(0:real)<2‚Äô by fs[] >> ‚Äò0<M‚Äô by metis_tac[RPOW_POS_LT] >>
            ‚Äò((&TBL2N i) :real) < &(TBL2N j + 1)‚Äô by fs[REAL_LT_RMUL] >>
            ‚Äò((&TBL2N j):real) < &(TBL2N i + 1)‚Äô by fs[REAL_LT_RMUL] >> fs[]) >>
      wlog_tac ‚ÄòLENGTH i < LENGTH j‚Äô [‚Äòi‚Äô,‚Äòj‚Äô,‚Äòx‚Äô]
      >- (‚ÄòLENGTH j < LENGTH i‚Äô by simp[] >> metis_tac[]) >>
      qabbrev_tac‚ÄòD=LENGTH j ‚àí LENGTH i‚Äô >> qabbrev_tac‚Äòii= TBL2N i‚Äô >>
      qabbrev_tac‚Äòjj= TBL2N j‚Äô >>
      ‚Äòi ‚â∫ j‚Äôsuffices_by simp[] >>
      irule disjoint_pf_lem1 >>
      full_simp_tac (srw_ss() ++ realSimps.REAL_ARITH_ss)
                    [powr_negexp,
                     RealArith.REAL_ARITH ‚Äú(x:real) * y + y = (x + 1) * y‚Äù] >>
      ‚Äò&ii * 2 pow LENGTH j ‚â§ x * 2 pow LENGTH i * 2 pow LENGTH j‚Äô by simp[] >>
      ‚Äòx * 2 pow LENGTH i * 2 pow LENGTH j < &(jj + 1) * 2 pow LENGTH i‚Äô
        by simp[] >>
      dxrule_all_then strip_assume_tac REAL_LET_TRANS >>
      dxrule_then (qspec_then ‚Äòinv (2 pow LENGTH i)‚Äô mp_tac) REAL_LT_RMUL_IMP >>
      impl_tac >- simp[REAL_POW_POS] >>
      REWRITE_TAC [GSYM REAL_MUL_ASSOC, REAL_INV_nonzerop, GSYM REAL_POW_INV] >>
      ASM_SIMP_TAC bool_ss [pow_inv_mul_invlt] >>
      simp[REAL_OF_NUM_POW, DECIDE ‚Äúx < y + 1n ‚áî x ‚â§ y‚Äù] >> strip_tac >>
      ‚Äò&jj * 2 pow LENGTH i ‚â§ x * 2 pow LENGTH i * 2 pow LENGTH j‚Äô by simp[] >>
      ‚Äòx * 2 pow LENGTH i * 2 pow LENGTH j < &(ii + 1) * 2 pow LENGTH j‚Äô
        by simp[] >>
      dxrule_all_then strip_assume_tac REAL_LET_TRANS >>
      dxrule_then (qspec_then ‚Äòinv (2 pow LENGTH i)‚Äô mp_tac) REAL_LT_RMUL_IMP >>
      impl_tac >- simp[REAL_POW_POS] >>
      REWRITE_TAC [GSYM REAL_MUL_ASSOC, REAL_INV_nonzerop, GSYM REAL_POW_INV] >>
      ASM_SIMP_TAC bool_ss [pow_inv_mul_invlt] >> simp[] >>
      simp[REAL_OF_NUM_POW])
  >- (fs[prefix_free_def,PULL_EXISTS] >> rw[] >>
      strip_tac >>
      ‚Äòinterval_bl a <> interval_bl b‚Äô by
        (fs[interval_bl_def] >> rw[] >>
         ‚ÄòLENGTH a < LENGTH b‚Äô by simp[prefix_length_lt]>>
         ‚Äòreal_of_num (LENGTH a) < real_of_num (LENGTH b)‚Äô by fs[REAL_LT] >>
         ‚Äò-(real_of_num (LENGTH b)) < -(real_of_num(LENGTH a))‚Äô
           by fs[REAL_LT_NEG] >>
         ‚Äò1<(2:real)‚Äô by simp[]>>
         ‚Äò2 rpow -&LENGTH b < 2 rpow -&LENGTH a‚Äô by fs[RPOW_LT]>>
         fs[REAL_LT_IMP_NE])>>
      first_x_assum drule_all >>
      fs[interval_bl_def,disjoint_interval_def,
         churchDBTheory.DISJ_IMP_EQ] >> rw[DISJOINT_DEF,EXTENSION] >>
      qexists_tac‚Äò&TBL2N b * 2 rpow -&LENGTH b‚Äô >> rw[]
      >- (fs[powr_negexp, prefix_append,TBL2N_append, REAL_OF_NUM_POW,
             RIGHT_ADD_DISTRIB] >>
          REWRITE_TAC [GSYM MULT_ASSOC, arithmeticTheory.EXP_ADD] >> simp[])
      >- (fs[powr_negexp, prefix_append,TBL2N_append] >>
          simp[POW_ADD, REAL_LDISTRIB] >> simp[REAL_OF_NUM_POW])
      >- (simp[powr_negexp, REAL_LDISTRIB]))
QED

Theorem size_of_interval_bl:
  Normal (SND (interval_bl y))  = Normal (2 rpow -&LENGTH y)
Proof rw[interval_bl_def]
QED

Theorem TBL2N_append_sing_ub:
  &(TBL2N (a++[h]) + 1)*2 rpow (-1) <= &(TBL2N a + 1)
Proof
  ‚Äò&(2 * TBL2N a + 2) * 2 rpow -1 <= &(TBL2N a + 1)‚Äô
    by (‚Äò&(2 * TBL2N a + 2) * 2 rpow -1 = &(2*(TBL2N a + 1)) * 2 rpow -1‚Äô
          by fs[GSYM LEFT_ADD_DISTRIB]>>
        ‚Äò_ = 2 * &(TBL2N a + 1) * 2 rpow -1‚Äô by fs[REAL_MUL] >>
        ‚Äò_ = &(TBL2N a + 1)*(2 rpow 1 * 2 rpow -1)‚Äô
          by fs[AC REAL_MUL_ASSOC REAL_MUL_COMM,RPOW_1]>>
        ‚Äò_ = &(TBL2N a + 1) * 2 rpow (1+ -1)‚Äô by fs[RPOW_ADD] >>
        fs[RPOW_0,REAL_ADD_RINV]  ) >>
  Cases_on‚Äòh‚Äô >> rw[TBL2N_append] >>
  ‚Äò&(2 * TBL2N a + 1) * 2 rpow -1 <= &(2 * TBL2N a + 2) * 2 rpow -1‚Äô
    by (irule REAL_LE_RMUL_IMP >> rw[RPOW_POS_LT,REAL_LE_LT]) >>
  irule REAL_LE_TRANS >> qexists_tac‚Äò&(2 * TBL2N a + 2) * 2 rpow -1‚Äô >> rw[]
QED

Theorem interval_bl_bounds:
  0 ‚â§ FST (interval_bl a) /\ FST (interval_bl a) + SND (interval_bl a) ‚â§ 1
Proof
  rw[]
  >- simp[interval_bl_def, powr_negexp]
  >- (simp[interval_bl_def, powr_negexp,
           RealArith.REAL_ARITH ‚Äúx * y + y :real = (x + 1) * y‚Äù] >>
      simp[REAL_OF_NUM_POW] >>
      (TBL2N_ub |> Q.INST [‚Äòl‚Äô|->‚Äòa‚Äô] |> assume_tac) >> simp[])
QED

Definition bls_size_def:
  bls_size (P:bool list -> bool) n =
  SIGMA (\s. (2 rpow -&LENGTH s)) {x|x IN P /\ LENGTH x < n }
End

Theorem finite_and:
  FINITE {s| P s} ==> FINITE {s|  Q s /\ P s}
Proof
  rw[] >> ‚Äò{s | Q s /\ P s} = {s | Q s}  INTER {s | P s}‚Äô by fs[INTER_DEF] >>
  fs[FINITE_INTER]
QED

Theorem interval_bl_sigma:
  FINITE  {x | x IN P} ==>
  SIGMA (\a. SND (interval_bl a)) {x | x IN P} =
  SIGMA (\a. 2 rpow -&LENGTH a) {x | x IN P}
Proof
  rw[] >> irule REAL_SUM_IMAGE_EQ >> rw[] >> simp[interval_bl_def]
QED

fun fsr thl = full_simp_tac(srw_ss () ++ realSimps.REAL_ARITH_ss) thl;

Theorem disjoint_interval_thm:
  disjoint_interval (interval_bl i) (interval_bl j) ==>
  FST (interval_bl i) + SND (interval_bl i) <= FST (interval_bl j) \/
  FST (interval_bl j) + SND (interval_bl j) <= FST (interval_bl i)
Proof
  rw[interval_bl_def,disjoint_interval_def,DISJOINT_DEF,EXTENSION,
     PULL_EXISTS] >>
  Cases_on‚Äò&TBL2N i * 2 rpow -&LENGTH i + 2 rpow -&LENGTH i <
           &TBL2N j * 2 rpow -&LENGTH j‚Äô >>
  fsr[]>>
  qabbrev_tac‚Äòid=2 rpow -&LENGTH i‚Äô >> qmatch_abbrev_tac‚Äò_ \/ jj+jd<=ii:real‚Äô >>
  spose_not_then assume_tac >>
  fsr[REAL_NOT_LT,REAL_NOT_LE]  >>
  first_x_assum(fn th => qspec_then‚Äòjj‚Äô assume_tac th >>
                qspec_then‚Äòii‚Äô assume_tac th) >> fsr[]
  >- (‚Äòid<= 0‚Äô by metis_tac[REAL_LE_LADD,REAL_ADD_RID] >>
      ‚Äò0<id‚Äô suffices_by metis_tac[REAL_LET_TRANS,REAL_LT_REFL] >>
      simp[Abbr‚Äòid‚Äô,RPOW_POS_LT])
  >- (‚Äòjd<= 0‚Äô by metis_tac[REAL_LE_LADD,REAL_ADD_RID] >>
      ‚Äò0<jd‚Äô suffices_by metis_tac[REAL_LET_TRANS,REAL_LT_REFL] >>
      simp[Abbr‚Äòjd‚Äô,RPOW_POS_LT])
QED

Triviality max_rs_lemma:
  !s. FINITE s ==> s<>{} ==> ?x:real. x IN s /\ !y. y IN s ==> y<=x
Proof
  Induct_on‚ÄòFINITE‚Äô >>rw[] >> Cases_on‚Äòs={}‚Äô >> fs[] >> qexists_tac‚Äòmax e x‚Äô >>
  fsr[max_def] >> rw[]
  >- metis_tac[] >> Cases_on‚Äòy=e‚Äô>> fsr[] >> RES_TAC >> fsr[]
QED

Triviality min_rs_lemma:
  !s. FINITE s ==> s<>{} ==> ?x:real. x IN s /\ !y. y IN s ==> x<=y
Proof
  Induct_on‚ÄòFINITE‚Äô >>rw[] >> Cases_on‚Äòs={}‚Äô >> fs[] >> qexists_tac‚Äòmin e x‚Äô >>
  fsr[min_def] >> rw[]>>
  Cases_on‚Äòy=e‚Äô>> fsr[] >> RES_TAC >> fsr[]
QED

Theorem maxr_set_def = new_specification(
  "maxr_set_def",["maxr_set"],
  SIMP_RULE(srw_ss() )[SKOLEM_THM,GSYM RIGHT_EXISTS_IMP_THM] max_rs_lemma
  );

Theorem minr_set_def = new_specification(
  "minr_set_def",["minr_set"],
  SIMP_RULE(srw_ss() )[SKOLEM_THM,GSYM RIGHT_EXISTS_IMP_THM] min_rs_lemma);

Definition exten_def:
  exten s = (minr_set {FST k| k IN s},maxr_set {FST k + SND k|k IN s})
End

Theorem maxr_set_thm[simp]:
  maxr_set {e} = e /\
  (FINITE s /\ s<> {} ==> maxr_set (e INSERT s) = max e (maxr_set s))
Proof
  rw[] >- (qspec_then ‚Äò{e}‚Äô mp_tac maxr_set_def >> simp[]) >>
  qspec_then ‚Äòe INSERT s‚Äô mp_tac maxr_set_def  >>
  simp[churchDBTheory.DISJ_IMP_EQ] >> strip_tac >>
  qabbrev_tac‚Äòm = maxr_set (e INSERT s)‚Äô >> qspec_then‚Äòs‚Äô mp_tac maxr_set_def >>
  simp[] >>
  qabbrev_tac‚Äòm0 = maxr_set s‚Äô >> rw[] >> rw[max_def]
  >- (Cases_on‚Äòe = m0‚Äô >> rw[] >- (simp[Abbr‚Äòm‚Äô,ABSORPTION_RWT]) >>
      Cases_on‚Äòe=m‚Äô >> rw[]
      >- (‚Äòm0<=e‚Äô by metis_tac[] >> fsr[]) >> metis_tac[REAL_LE_ANTISYM]  ) >>
  spose_not_then strip_assume_tac >> ‚Äòm<=m0‚Äô by fsr[] >> ‚Äòm<e‚Äô by fsr[] >>
  ‚Äòe<=m‚Äô by fsr[] >> fsr[]
QED

Theorem minr_set_thm[simp]:
  minr_set {e} = e /\
  (FINITE s /\ s<> {} ==> minr_set (e INSERT s) = min e (minr_set s))
Proof
  rw[] >- (qspec_then ‚Äò{e}‚Äô mp_tac minr_set_def >> simp[]) >>
  qspec_then ‚Äòe INSERT s‚Äô mp_tac minr_set_def  >>
  simp[churchDBTheory.DISJ_IMP_EQ] >> strip_tac >>
  qabbrev_tac‚Äòm = minr_set (e INSERT s)‚Äô >> qspec_then‚Äòs‚Äô mp_tac minr_set_def >>
  simp[] >>
  qabbrev_tac‚Äòm0 = minr_set s‚Äô >> rw[] >> rw[min_def]
  >- (Cases_on‚Äòe = m0‚Äô >> rw[] >- (simp[Abbr‚Äòm‚Äô,ABSORPTION_RWT]) >>
      Cases_on‚Äòe=m‚Äô >> rw[] >>
      ‚Äòm<=e‚Äô by metis_tac[] >>‚Äòm IN s‚Äô by fs[] >>‚Äòm0<=m‚Äô by fs[] >>
      metis_tac[REAL_LE_ANTISYM]) >>
  spose_not_then strip_assume_tac >> ‚Äòm<=m0‚Äô by fsr[] >> ‚Äòm0<e‚Äô by fsr[] >>
  ‚Äòm<=e‚Äô by fsr[] >>
  Cases_on‚Äòm=e‚Äô >> fsr[] >> ‚Äòm0 <= m‚Äô by fsr[] >> fsr[]
QED

Theorem maxr_set_union:
  !s1. FINITE s1 ==>
       !s2. FINITE s2 ==>
            s1 <> EMPTY /\ s2 <> EMPTY ==>
            maxr_set (s1 UNION s2)=max (maxr_set s1) (maxr_set s2)
Proof
Induct_on‚ÄòFINITE‚Äô >> strip_tac
>- (Induct_on‚ÄòFINITE‚Äô >>rw[maxr_set_thm] )
>- (ntac 4 strip_tac >> Induct_on‚ÄòFINITE‚Äô >> rw[maxr_set_thm] >>
    Cases_on‚Äòs2 <> {}‚Äô >> Cases_on‚Äòs1 <> {}‚Äô >> rw[] >>simp[maxr_set_thm]
    >- (Cases_on‚Äòe<= maxr_set s1‚Äô >> Cases_on‚Äòe'<= maxr_set s2‚Äô >>
        fs[max_def,maxr_set_thm,INSERT_UNION_EQ] >> rw[] >> fsr[]
        >- (Cases_on‚Äòmaxr_set s1 <= maxr_set s2‚Äô >> fsr[])
        >- (Cases_on‚Äòmaxr_set s1 <= e'‚Äô >> fsr[]) )
    >- (Cases_on‚Äòe'<= maxr_set s2‚Äô >>
        fs[max_def,maxr_set_thm,INSERT_UNION_EQ] >> rw[] >> fsr[])
    >- (Cases_on‚Äòe<= maxr_set s1‚Äô >> fs[max_def,maxr_set_thm,INSERT_UNION_EQ] >> rw[] >>
        fsr[] >> Cases_on‚Äòmaxr_set s1 <= e'‚Äô >> fsr[])
    >- (fs[max_def,maxr_set_thm,INSERT_UNION_EQ]) )
QED


Theorem minr_set_union:
  !s1. FINITE s1 ==>
       !s2. FINITE s2 ==> s1 <> EMPTY /\ s2 <> EMPTY ==>
            minr_set (s1 UNION s2)=min (minr_set s1) (minr_set s2)
Proof
Induct_on‚ÄòFINITE‚Äô >> strip_tac
>- (Induct_on‚ÄòFINITE‚Äô >>rw[minr_set_thm] )
>- (ntac 4 strip_tac >> Induct_on‚ÄòFINITE‚Äô >> rw[minr_set_thm] >>
    Cases_on‚Äòs2 <> {}‚Äô >> Cases_on‚Äòs1 <> {}‚Äô >> rw[] >>simp[minr_set_thm]
    >- (Cases_on‚Äòe<= minr_set s1‚Äô >> Cases_on‚Äòe'<= minr_set s2‚Äô >>
        fs[min_def,minr_set_thm,INSERT_UNION_EQ] >> rw[] >> fsr[]
        >- (Cases_on‚Äòminr_set s1 <= e'‚Äô >> fsr[])
        >- (Cases_on‚Äòminr_set s1 <= minr_set s2‚Äô >> fsr[]) )
    >- (Cases_on‚Äòe<= minr_set s1‚Äô >> fs[min_def,minr_set_thm,INSERT_UNION_EQ] >>
        rw[] >> fsr[] >> Cases_on‚Äòminr_set s1 <= e'‚Äô >> fsr[])
    >- (Cases_on‚Äòe<= minr_set s1‚Äô >> fs[min_def,minr_set_thm,INSERT_UNION_EQ] >>
        rw[] >> fsr[] >> Cases_on‚Äò minr_set s1 <= e'‚Äô >>rw[] >> fsr[])
    >- (fs[min_def,minr_set_thm,INSERT_UNION_EQ]) )
QED

Theorem gspec_eq[simp]: GSPEC (\x. (f x,x=k)) = {f k}
Proof simp[EXTENSION]
QED

Theorem gspec_f_or[simp]:
  !P Q. {f x | P x \/ Q x} = {f x | P x} UNION {f x | Q x}
Proof
  rw[IN_UNION,EXTENSION] >> metis_tac[GSPECIFICATION_applied]
QED

Theorem exten_insert_thm:
  s ‚â† ‚àÖ /\ FINITE s ==>
  exten (e INSERT s) =  (min (FST e) ## max (FST e + SND e)) (exten s)
Proof
  simp[exten_def,maxr_set_thm,minr_set_thm] >> rw[]
  >- (‚ÄòFINITE {FST e}‚Äô by fs[] >> ‚Äò{FST e} <> {}‚Äô by fs[] >>
      ‚ÄòFINITE {FST k | k IN s}‚Äô by metis_tac[IMAGE_FINITE,IMAGE_DEF] >>
      ‚Äò{FST k | k IN s} <> {}‚Äô by metis_tac[IMAGE_EQ_EMPTY,IMAGE_DEF]>>
      rw[minr_set_union])
  >- (‚ÄòFINITE {FST e  + SND k}‚Äô by fs[] >> ‚Äò{FST e  + SND k} <> {}‚Äô by fs[] >>
      ‚ÄòFINITE {FST k + SND k | k IN s}‚Äô by metis_tac[IMAGE_FINITE,IMAGE_DEF] >>
      ‚Äò{FST k + SND k | k IN s} <> {}‚Äô by metis_tac[IMAGE_EQ_EMPTY,IMAGE_DEF]>>
      rw[maxr_set_union] )
QED

Theorem TBL2N_ub2:
  &TBL2N x * 2 rpow -&LENGTH x +  2 rpow -&LENGTH x <= 1
Proof
  simp[powr_negexp, RealArith.REAL_ARITH ‚Äú(x:real) * y + y = (x + 1) * y‚Äù,
       REAL_OF_NUM_POW] >>
  (TBL2N_ub |> Q.INST [‚Äòl‚Äô|->‚Äòx‚Äô] |> assume_tac) >> simp[TBL2N_ub]
QED

val rpow_len_lb = Q.store_thm("rpow_len_lb",
‚Äò0<  2 rpow -&LENGTH x‚Äô,
fs[RPOW_POS_LT]);

Theorem TBL2N_lb:
  0 <= &TBL2N x * 2 rpow -&LENGTH x
Proof
  simp[powr_negexp]
QED

Theorem lemma11:
  !s. FINITE s ==>
      (!x dx y dy. (x,dx) IN s /\ (y,dy) IN s /\ x<>y ==> x+dx<=y \/ y+dy<=x) /\
      (!x d. (x,d) IN s ==> 0r<d) /\ (!x d. (x,d) IN s ==> 0<= x /\ x+d <=1) /\
      (s <> {})
      ==> 0 <= FST (exten s) /\ SND (exten s) <= 1
Proof
  Induct_on‚ÄòFINITE‚Äô >> simp[] >>ntac 2 (gen_tac>>strip_tac) >>strip_tac >>
  Cases_on‚Äòs={}‚Äô >> fs[]
  >- (simp[maxr_set_def,minr_set_def,exten_def,ITSET_THM] >> Cases_on‚Äòe‚Äô >>
      fs[]) >>
  fs[exten_insert_thm] >> rw[max_def,min_def]
  >- (Cases_on‚Äòe‚Äô >> fsr[] >> metis_tac[])
  >- metis_tac[]
  >- metis_tac[]
QED

Theorem BETTER_RPOW_UNIQ_EXP[simp]:
  1r < a ==> (a rpow b = a rpow c <=> b = c)
Proof
  rw[] >> eq_tac >> simp[] >> rw[] >>
  pop_assum (mp_tac o AP_TERM ‚Äútransc$ln‚Äù) >> fsr[LN_RPOW] >>
  strip_tac >>
  ‚Äòln 1 < ln a‚Äô by fsr[LN_MONO_LT] >> fsr[LN_1]
QED

Theorem interval_bl_11[simp]:
  interval_bl x = interval_bl y <=> x = y
Proof
  simp[interval_bl_def] >> eq_tac >> simp[] >> rw[] >>
  fsr[TBL2N_inj_len,RPOW_NZ]
QED

Theorem interval_bl_inj:
  INJ interval_bl P (IMAGE interval_bl P)
Proof
  simp[INJ_DEF]
QED

Theorem lemma12[local]:
  bls_size P n = SIGMA SND (IMAGE interval_bl {s|s IN P /\ LENGTH s < n})
Proof
  fs[bls_size_def,interval_bl_def,REAL_SUM_IMAGE_IMAGE,interval_bl_inj,
     finite_bool_list_lt_n,finite_and,combinTheory.o_DEF]
QED

Theorem exten_sing[simp]:
  exten {e} = (FST e,FST e + SND e)
Proof fs[exten_def]
QED

Theorem size_of_exten[local]:
  !s. FINITE s ==>
      (!x dx y dy. (x,dx) IN s /\ (y,dy) IN s /\ (x,dx)<>(y,dy) ==>
                   x+dx<=y \/ y+dy<=x) /\
      (!x d. (x,d) IN s ==> 0r<d) /\ (s <> {})
      ==> SIGMA SND s <= SND (exten s) - FST (exten s)
Proof
  HO_MATCH_MP_TAC FINITE_COMPLETE_INDUCTION >> rw[] >>
  map_every qabbrev_tac [
      ‚ÄòRs = {FST k + SND k | k IN s}‚Äô,‚ÄòLs = {FST k |k IN s}‚Äô] >>
  ‚ÄòFINITE Ls‚Äô by metis_tac[IMAGE_FINITE,IMAGE_DEF] >>
  ‚ÄòLs <> {}‚Äô by metis_tac[IMAGE_EQ_EMPTY,IMAGE_DEF] >>
  ‚Äò?e1 e2. (e1,e2) IN s /\ !m. m IN Ls ==> e1<=m‚Äô
    by (qexists_tac ‚Äòminr_set Ls‚Äô >> qexists_tac‚Äò@e'. (minr_set Ls,e') IN s‚Äô >>
        reverse(rw[])
        >- (metis_tac[minr_set_def]) >> SELECT_ELIM_TAC >> simp[] >>
        ‚Äòminr_set Ls IN Ls‚Äô by metis_tac[minr_set_def] >> fs[Abbr‚ÄòLs‚Äô] >>
        metis_tac[pairTheory.PAIR]) >>
  qabbrev_tac‚Äòs0 = s DELETE (e1,e2)‚Äô >> Cases_on‚Äòs0={}‚Äô >> simp[]
  >- (‚Äòs = {(e1,e2)}‚Äô by (metis_tac[DELETE_EQ_SING]) >>
      rw[REAL_SUM_IMAGE_THM] >> fsr[]) >>
  ‚Äòs0 ‚äÇ s‚Äô by (fs[Abbr‚Äòs0‚Äô,PSUBSET_MEMBER]>> metis_tac[]) >>
  last_x_assum drule >> impl_tac
  >- (rw[Abbr‚Äòs0‚Äô] >> metis_tac[]) >> ‚Äòs = (e1,e2) INSERT s0‚Äô by fs[Abbr‚Äòs0‚Äô] >>
  ‚Äòs0 DELETE (e1,e2) = s0‚Äô by fs[Abbr‚Äòs0‚Äô,ELT_IN_DELETE,DELETE_NON_ELEMENT] >>
  fsr[REAL_SUM_IMAGE_THM] >>
  ‚ÄòSND (exten s0) - FST (exten s0) ‚â§
   SND (exten ((e1,e2) INSERT s0)) ‚àí FST (exten ((e1,e2) INSERT s0)) - e2‚Äô
    suffices_by fsr[] >>
  fsr[exten_def,exten_insert_thm,REAL_SUM_IMAGE_THM,DELETE_NON_ELEMENT_RWT] >>
  map_every qabbrev_tac [‚ÄòMX = maxr_set {FST k + SND k | k IN s0}‚Äô,
                         ‚ÄòMN = minr_set  {FST k | k IN s0}‚Äô]>>
  map_every qabbrev_tac[‚ÄòRs0 = {FST k + SND k | k IN s0}‚Äô,
                        ‚ÄòLs0 = {FST k |k IN s0}‚Äô] >>
  ‚ÄòFINITE Ls0‚Äô by metis_tac[IMAGE_FINITE,IMAGE_DEF] >>
  ‚ÄòLs0 <> {}‚Äô by metis_tac[IMAGE_EQ_EMPTY,IMAGE_DEF] >>
  ‚ÄòMN IN Ls0‚Äô by metis_tac[minr_set_def] >>
  ‚Äò?n. (MN,n) IN s0‚Äô by
    (‚Äò?p. p IN s0 /\ FST p = MN‚Äô suffices_by
       (rw[] >> metis_tac[pairTheory.PAIR]) >>
     pop_assum MP_TAC >> simp[Abbr‚ÄòLs0‚Äô] >> metis_tac[]) >>
  ‚ÄòMN IN Ls‚Äô by (simp[Abbr‚ÄòLs‚Äô] >>metis_tac[pairTheory.FST]) >>
  rw[max_def,min_def] >> fsr[]
  >- (‚Äòe1 + e2 <= MN‚Äô suffices_by fsr[] >>
      first_x_assum(qspecl_then[‚Äòe1‚Äô,‚Äòe2‚Äô,‚ÄòMN‚Äô,‚Äòn‚Äô] MP_TAC) >> simp[] >>
      impl_tac
      >- (rpt strip_tac >> rw[] >> rw[Abbr‚Äòs0‚Äô] >> fs[]) >> strip_tac >>
      ‚Äòe1<=MN‚Äô by fs[]>>
      ‚Äò0<n‚Äô suffices_by fsr[] >> metis_tac[]  )
  >- (‚ÄòMX < e1+e2‚Äô by fsr[] >>
      ‚ÄòFINITE Rs0‚Äô by metis_tac[IMAGE_FINITE,IMAGE_DEF] >>
      ‚ÄòRs0 <> {}‚Äô by metis_tac[IMAGE_EQ_EMPTY,IMAGE_DEF] >>
      ‚ÄòMX IN Rs0‚Äô by metis_tac[maxr_set_def]>>
      ‚Äò?i j. (i,j) IN s0 /\ MX = i+j‚Äô by (pop_assum MP_TAC >> simp[Abbr‚ÄòRs0‚Äô] >>
                                          metis_tac[pairTheory.PAIR]) >>
      first_x_assum(qspecl_then[‚Äòe1‚Äô,‚Äòe2‚Äô,‚Äòi‚Äô,‚Äòj‚Äô] MP_TAC) >> simp[] >> impl_tac
      >- (rpt strip_tac >> rw[] >> rw[Abbr‚Äòs0‚Äô] >> fs[]) >> strip_tac
      >- (rw[] >> ‚Äò0<j‚Äô suffices_by fsr[] >> metis_tac[]) >> rw[] >>
      ‚Äòi IN Ls‚Äô
        by (simp[Abbr‚ÄòLs‚Äô,pairTheory.EXISTS_PROD, Abbr‚ÄòLs0‚Äô] >> metis_tac[])>>
      ‚Äòe1<=i‚Äô by simp[]>>
      ‚Äò0<j‚Äô suffices_by fsr[] >> metis_tac[]  )
QED

Theorem exten_member:
  FINITE s /\ s <> {} /\ exten (s) = (sn,sx) ==>
  ‚àÉsn' sx' dx. (sn,sn') IN s /\ (sx',dx) IN s /\ sx'+dx=sx
Proof
  simp[exten_def] >> rw[] >>
  ‚Äò{FST k|k IN s} = IMAGE FST s‚Äô by simp[EXTENSION] >>
  ‚ÄòFINITE {FST k|k IN s} /\ {FST k | k IN s} <> {}‚Äô by simp[] >>
  drule_all minr_set_def >>rw[] >>
  qexists_tac‚ÄòSND k‚Äô >> simp[] >>
  ‚Äò{FST k + SND k|k IN s} = IMAGE (\k. FST k+ SND k) s‚Äô by simp[EXTENSION] >>
  ‚ÄòFINITE {FST k + SND k|k IN s} /\ {FST k + SND k | k IN s} <> {}‚Äô by simp[] >>
  drule_all maxr_set_def >>
  rw[] >> rename[‚Äòmaxr_set _ = FST kk + SND kk‚Äô] >>
  map_every qexists_tac [‚ÄòFST kk‚Äô,‚ÄòSND kk‚Äô] >> simp[]
QED

Overload bpos = ‚ÄúŒªl. &TBL2N l * 2 rpow -&LENGTH l‚Äù

Theorem bpos_min:
  !s. FINITE s ==> s <> {} ==> ?l. l IN s /\ !m. m IN s ==> bpos l <= bpos m
Proof
  Induct_on‚ÄòFINITE‚Äô >> simp[] >> rw[] >> Cases_on‚Äòs={}‚Äô >> simp[] >>fs[] >>
  Cases_on‚Äòbpos e <= bpos l‚Äô>>
  fs[] >- (qexists_tac‚Äòe‚Äô >> simp[] >> metis_tac[REAL_LE_TRANS,REAL_LE_REFL]) >>
  qexists_tac‚Äòl‚Äô >> simp[] >> metis_tac[REAL_NOT_LE,REAL_LT_IMP_LE]
QED

Theorem exten_low_high:
  !s. FINITE s ‚àß s <> {} ==>
      !low high. exten (IMAGE interval_bl s) = (low,high) ==> low <= high
Proof
  Induct_on‚ÄòFINITE‚Äô >> simp[] >> rw[] >> Cases_on‚Äòs={}‚Äô >> simp[] >>fs[]
  >- (fs[interval_bl_def] >> rw[REAL_LE_ADDR] >> rw[RPOW_POS_LT,REAL_LE_LT]) >>
  qpat_x_assum‚Äòexten _ = _‚Äô mp_tac >> rfs[exten_insert_thm] >>
  Cases_on‚Äòexten (IMAGE interval_bl s)‚Äô >>
  fs[] >> fsr[interval_bl_def,min_def,max_def] >> rw[]>>fsr[]
QED

Theorem lemma13:
  ‚àÄP. FINITE P ‚áí
      P ‚â† ‚àÖ ‚àß
      (!x y. x ‚àà P ‚àß y ‚àà P ‚àß x ‚â† y ==>
             disjoint_interval (interval_bl x) (interval_bl y)) ==>
      SIGMA (\s. 2 rpow -&LENGTH s) P + FST (exten (IMAGE interval_bl P) )
       <= SND (exten (IMAGE interval_bl P))
Proof
  HO_MATCH_MP_TAC FINITE_COMPLETE_INDUCTION >> rw[] >>
  qmatch_abbrev_tac‚Äò_ <= SND (exten s)‚Äô >>
  ‚Äò?l. l IN P /\ !m. m IN P ==> bpos l <= bpos m‚Äô by metis_tac[bpos_min] >>
  qabbrev_tac‚ÄòP0 = P DELETE l‚Äô >> Cases_on‚ÄòP0={}‚Äô >> simp[]
  >- (‚ÄòP = {l}‚Äô by metis_tac[DELETE_EQ_SING] >>
      fsr[REAL_SUM_IMAGE_THM,Abbr‚Äòs‚Äô,interval_bl_def,TBL2N_lb,Abbr‚ÄòP0‚Äô]) >>
  ‚ÄòP0 ‚äÇ P‚Äô by (fs[Abbr‚ÄòP0‚Äô,PSUBSET_MEMBER]>> metis_tac[]) >>
  last_x_assum drule >> impl_tac
  >- (rw[Abbr‚ÄòP0‚Äô] >> metis_tac[]) >> ‚ÄòP = l INSERT P0‚Äô by fs[Abbr‚ÄòP0‚Äô] >>
  ‚ÄòP0 DELETE l = P0‚Äô by fs[Abbr‚ÄòP0‚Äô,ELT_IN_DELETE,DELETE_NON_ELEMENT] >>
  fsr[REAL_SUM_IMAGE_THM,Abbr‚Äòs‚Äô,exten_insert_thm] >>
  ‚Äò‚àÉen ex. exten (IMAGE interval_bl P0) = (en,ex)‚Äô
    by metis_tac[pairTheory.pair_CASES] >> simp[]>>
  ‚Äò‚àÉl0 d. interval_bl l = (l0,d)‚Äô by metis_tac[pairTheory.pair_CASES] >> fs[] >>
  ‚Äò2 rpow -&LENGTH l = d‚Äô by fsr[interval_bl_def] >> fs[] >>
  ‚Äòmax (l0 + d) ex = d + max l0 (ex-d)‚Äô by (fsr[max_def] >> rw[] >> fsr[]) >>
  fsr[GSYM REAL_ADD_ASSOC]>>
  ‚Äò‚àÉen'. (en,en') ‚àà IMAGE interval_bl P0‚Äô
    by metis_tac[exten_member,IMAGE_FINITE,IMAGE_EQ_EMPTY]>> fs[]>>
  rename[‚Äò(en,en') = interval_bl EN‚Äô] >>
  ‚Äòl0+d<=en‚Äô
    by (first_x_assum (qspecl_then [‚Äòl‚Äô,‚ÄòEN‚Äô] mp_tac) >>
        simp[] >> impl_tac >- (strip_tac >> fs[GSYM DELETE_NON_ELEMENT])  >>
        qpat_assum‚Äòinterval_bl _ = _‚Äô (SUBST1_TAC o SYM) >> strip_tac >>
        drule disjoint_interval_thm >>
        qpat_x_assum‚Äò_ = interval_bl _‚Äô (assume_tac o SYM)>>
        simp[] >> strip_tac >>
        ‚Äò&TBL2N l * d ‚â§ bpos EN‚Äô by simp[] >> fs[interval_bl_def] >>rw[] >>
        ‚Äò0<2 rpow -&LENGTH EN‚Äô suffices_by fsr[] >> fs[RPOW_POS_LT]) >>
  ‚Äò0 < d‚Äô by (rw[] >> fsr[RPOW_POS_LT]) >>
  ‚Äòl0 ‚â§ en‚Äô by fsr[] >> simp[min_def] >>
  ‚Äòen ‚â§ ex‚Äô by metis_tac[exten_low_high] >>
  fsr[max_def]
QED

Theorem lemma1[local]:
  (let is = IMAGE interval_bl P
   in
     !i1 i2. i1 IN is /\ i2 IN is /\ i1<>i2 ==> disjoint_interval i1 i2)
  ==>
  !n. bls_size P n <= 1
Proof
  rw[EQ_IMP_THM,PULL_EXISTS] >> fs[bls_size_def,disjoint_interval_thm] >>
  ‚ÄòFINITE  {x | x IN P /\ LENGTH x < n}‚Äô
    by metis_tac[finite_and,finite_bool_list_lt_n] >>
  ‚ÄòFINITE {(\y. (&TBL2N y * 2 rpow -&LENGTH y,2 rpow -&LENGTH y)) x |
           x IN {z | z IN P /\ LENGTH z <n}}‚Äô
    by metis_tac[IMAGE_FINITE,IMAGE_DEF] >> fs[] >>
  qabbrev_tac‚Äò
   s = {(&TBL2N x * 2 rpow -&LENGTH x,2 rpow -&LENGTH x) |
        x IN P /\ LENGTH x < n}‚Äô >>
  Cases_on‚Äòs={}‚Äô
  >- (‚Äò{x | x IN P /\ LENGTH x < n} = {}‚Äô
        by (fs[Abbr‚Äòs‚Äô] >>
            ‚Äò{(\y. (&TBL2N y * 2 rpow -&LENGTH y,2 rpow -&LENGTH y))  x |
              x IN P /\ LENGTH x < n} = {}‚Äô
              by simp[] >>
            ‚ÄòIMAGE (\x. (&TBL2N x * 2 rpow -&LENGTH x,2 rpow -&LENGTH x))
               {x | x IN P /\ LENGTH x < n} =
             {(\y. (&TBL2N y * 2 rpow -&LENGTH y,2 rpow -&LENGTH y))  x |
              x IN P /\ LENGTH x < n}‚Äô
              by fs[IMAGE_DEF] >> metis_tac[IMAGE_EQ_EMPTY]) >>
      fs[REAL_SUM_IMAGE_THM] ) >>
  ‚Äò0<= FST (exten s) /\ SND (exten s) <= 1‚Äô by
    (irule lemma11 >> rw[]>> fsr[Abbr‚Äòs‚Äô,TBL2N_lb,rpow_len_lb,TBL2N_ub2] >>
     rw[] >> ‚Äòx'<>x''‚Äô by (metis_tac[FUN_EQ_THM]) >>
     ‚Äòdisjoint_interval (interval_bl x') (interval_bl x'')‚Äô by simp[] >>
     ‚ÄòFST (interval_bl x') + SND (interval_bl x') <= FST (interval_bl x'') \/
      FST (interval_bl x'') + SND (interval_bl x'') <= FST (interval_bl x')‚Äô by
       simp[disjoint_interval_thm]  >> fs[interval_bl_def] ) >>
  qmatch_abbrev_tac‚ÄòSIGMA f sn <= 1r‚Äô>>
  ‚ÄòSIGMA f sn <= SND (exten s)‚Äô suffices_by fsr[] >>
  ‚Äòs = IMAGE interval_bl sn‚Äô
    by (fs[Abbr‚Äòsn‚Äô,Abbr‚Äòs‚Äô,interval_bl_def,IMAGE_DEF]) >> rw[] >>
  ‚Äò!x x'. x IN sn /\ x' IN sn /\ x <> x' ==>
          disjoint_interval (interval_bl x) (interval_bl x')‚Äô by
    (‚Äòsn = P INTER {x | LENGTH x < n}‚Äô by fs[INTER_DEF] >> rw[]) >>
  Cases_on‚Äòsn={}‚Äô >> fs[REAL_SUM_IMAGE_THM] >>
  ‚ÄòSIGMA f sn + FST (exten (IMAGE interval_bl sn)) <=
   SND (exten (IMAGE interval_bl sn))‚Äô by
    fs[Abbr‚Äòf‚Äô,lemma13] >> fsr[]
QED

Theorem kraft_ineq1: ‚àÄP. prefix_free P ==> (‚àÄn. bls_size P n ‚â§ 1)
Proof
(* Could change to `?y0. y0<=1 /\ bls_size P --> y0` *)
  metis_tac[disjoint_prefix_free,lemma1]
QED

Definition seq_size_def[simp]:
  seq_size f 0 = 0r ‚àß
  seq_size f (SUC n) = 2 rpow -(&f n) + seq_size f n
End

Theorem seq_size_positive[simp]:
  ‚àÄn. 0 ‚â§ seq_size f n
Proof
  Induct >> rw[] >> irule REAL_LE_ADD >> simp[REAL_LE_LT, RPOW_POS_LT]
QED

Theorem seq_size_EQ0[simp]:
  seq_size f n = 0 ‚áî n = 0
Proof
  Induct_on‚Äòn‚Äô >>
  simp[DECIDE ‚Äúx < SUC y ‚áî x < y ‚à® x = y‚Äù, DISJ_IMP_THM, FORALL_AND_THM] >>
  rw[] >>
  irule (RealArith.REAL_ARITH ‚Äú0r < x ‚àß 0 ‚â§ y ==> x + y ‚â† 0‚Äù) >>
  simp[powr_negexp, REAL_POW_POS]
QED

Theorem INFINITE_ALLCARD_SUBSETS:
  INFINITE s ‚áí ‚àÄn. ‚àÉs0. FINITE s0 ‚àß CARD s0 = n ‚àß s0 ‚äÜ s
Proof
  strip_tac >> Induct >- (qexists_tac ‚Äò‚àÖ‚Äô >> simp[]) >>
  gs[] >>
  ‚Äò‚àÉe. e ‚àà s ‚àß e ‚àâ s0‚Äô by metis_tac[IN_INFINITE_NOT_FINITE] >>
  qexists_tac ‚Äòe INSERT s0‚Äô >> simp[]
QED

Theorem seq_size_REAL_SUM_IMAGE:
  seq_size f n = REAL_SUM_IMAGE (Œªi. 2 rpow -&(f i)) (count n)
Proof
  Induct_on ‚Äòn‚Äô >> simp[REAL_SUM_IMAGE_THM, COUNT_SUC]
QED

Theorem seq_size_indices:
  (‚àÄm. seq_size f m ‚â§ 1) ‚áí
  ‚àÄn. FINITE {i | f i = n}  ‚àß CARD {i | f i = n} < 2 ** n
Proof
  strip_tac >> gen_tac >>
  ‚Äò‚àÄs0. FINITE s0 ‚àß s0 ‚äÜ { i | f i = n} ‚áí CARD s0 < 2 ** n‚Äô
    by (rw[] >> CCONTR_TAC >> gs[NOT_LESS] >>
        qabbrev_tac ‚Äòmx = MAX_SET s0‚Äô >>
        ‚Äòs0 ‚â† ‚àÖ‚Äô by (strip_tac >> gs[Abbr‚Äòmx‚Äô]) >>
        ‚Äòmx ‚àà s0 ‚àß ‚àÄj. j ‚àà s0 ‚áí j ‚â§ mx‚Äô by metis_tac[MAX_SET_DEF] >>
        ‚Äò1 < seq_size f (mx + 2)‚Äô suffices_by metis_tac[REAL_NOT_LE] >>
        simp[seq_size_REAL_SUM_IMAGE] >>
        ‚Äòs0 ‚äÜ count (mx + 2)‚Äô
          by (rw[count_def, SUBSET_DEF] >> first_x_assum drule >> simp[]) >>
        ‚Äòcount (mx + 2) = s0 ‚à™ (count (mx + 2) DIFF s0)‚Äô
          by simp[UNION_DIFF] >> pop_assum SUBST1_TAC >>
        ‚ÄòDISJOINT s0 (count (mx + 2) DIFF s0)‚Äô
          by (simp[DISJOINT_DEF, EXTENSION] >> metis_tac[]) >>
        simp[REAL_SUM_IMAGE_DISJOINT_UNION] >>
        ‚ÄòREAL_SUM_IMAGE (Œªi. 2 rpow -&f i) s0 = &CARD s0 * 2 rpow (- &n)‚Äô
          by (irule REAL_SUM_IMAGE_FINITE_CONST2 >> gs[SUBSET_DEF]) >>
        ‚Äò‚àÉd. CARD s0 = 2 ** n + d‚Äô by metis_tac[LESS_EQ_EXISTS] >>
        simp[REAL_LT_ADDR, powr_negexp, GSYM REAL_ADD, Excl "REAL_ADD",
             REAL_LDISTRIB] >> simp[REAL_OF_NUM_POW] >> fsr[] >>
        qmatch_abbrev_tac ‚Äò1r < AA + 1 + BB‚Äô >>
        ‚Äò0 ‚â§ AA ‚àß 0 < BB‚Äô suffices_by RealArith.REAL_ARITH_TAC >>
        simp[Abbr‚ÄòAA‚Äô, Abbr‚ÄòBB‚Äô] >>
        ‚Äòcount (mx + 2) DIFF s0 = (mx + 1) INSERT (count (mx + 1) DIFF s0)‚Äô
          by (simp[EXTENSION] >> qx_gen_tac ‚Äòi‚Äô >> Cases_on ‚Äòi ‚àà s0‚Äô >> simp[]>>
              first_x_assum drule >> simp[]) >>
        simp[REAL_SUM_IMAGE_THM] >>
        qmatch_abbrev_tac ‚Äò0r < AA + BB‚Äô >>
        ‚Äò0 < AA ‚àß 0 ‚â§ BB‚Äô suffices_by RealArith.REAL_ARITH_TAC >>
        simp[Abbr‚ÄòAA‚Äô, Abbr‚ÄòBB‚Äô] >> fsr[powr_negexp, REAL_OF_NUM_POW] >>
        irule REAL_SUM_IMAGE_POS >> fsr[]) >>
  csimp[] >> CCONTR_TAC >>
  drule_then (qspec_then ‚Äò2 ** n‚Äô strip_assume_tac)
             INFINITE_ALLCARD_SUBSETS >>
  metis_tac[prim_recTheory.LESS_REFL]
QED

Definition wfpfun_def:
  wfpfun f = { pf | prefix_free { w | ‚àÉi:num. pf i = SOME w } ‚àß
                    (‚àÄi p. pf i = SOME p ‚áí
                           LENGTH p = f i ‚àß
                           ‚àÄm j. m < LENGTH p ‚àß f j = m ‚áí ‚àÉq. pf j = SOME q) ‚àß
                    (‚àÄi j. pf i = NONE ‚àß j < i ‚àß f j = f i ‚áí pf j = NONE)
             }
End

Theorem wfpfun_nonempty:
  wfpfun f ‚â† ‚àÖ
Proof
  simp[wfpfun_def, EXTENSION] >> qexists_tac ‚ÄòK NONE‚Äô >> simp[]
QED

Definition pfunle_def:
  pfunle f = { (pf1, pf2) | pf1 ‚àà wfpfun f ‚àß pf2 ‚àà wfpfun f ‚àß
                            (‚àÄi. pf1 i ‚â† NONE ‚áí pf2 i = pf1 i) }
End

Theorem range_pfunle:
  range (pfunle f) = wfpfun f
Proof
  simp[set_relationTheory.range_def, EXTENSION, pfunle_def, EQ_IMP_THM,
       PULL_EXISTS] >> metis_tac[]
QED

Theorem domain_pfunle:
  domain (pfunle f) = wfpfun f
Proof
  simp[set_relationTheory.domain_def, EXTENSION, pfunle_def, EQ_IMP_THM,
       PULL_EXISTS] >> metis_tac[]
QED

Theorem partial_order_pfunle:
  partial_order (pfunle f) (wfpfun f)
Proof
  simp[set_relationTheory.partial_order_def, domain_pfunle, range_pfunle,
       set_relationTheory.transitive_def, set_relationTheory.reflexive_def,
       set_relationTheory.antisym_def] >>
  simp[pfunle_def, PULL_EXISTS] >> rw[] >>
  simp[FUN_EQ_THM] >> qx_gen_tac ‚Äòi‚Äô >> Cases_on ‚Äòx i‚Äô >> gs[] >>
  Cases_on ‚Äòy i‚Äô >> gs[]
QED

Theorem pfunle_chain_bounds:
  ‚àÄt. chain t (pfunle f) ‚áí upper_bounds t (pfunle f) ‚â† ‚àÖ
Proof
  simp[EXTENSION, set_relationTheory.upper_bounds_def, range_pfunle,
       set_relationTheory.chain_def] >> rw[] >>
  ‚Äò‚àÄpf. pf ‚àà t ‚áí pf ‚àà wfpfun f‚Äô
    by (gen_tac >> strip_tac >>
        first_x_assum $ qspecl_then [‚Äòpf‚Äô, ‚Äòpf‚Äô] mp_tac >> simp[] >>
        simp[pfunle_def]) >>
  qexists_tac ‚ÄòŒªi. case some pf. pf ‚àà t ‚àß pf i ‚â† NONE of
                    NONE => NONE
                  | SOME pf => pf i‚Äô >>
  conj_asm1_tac
  >- (simp[wfpfun_def, AllCaseEqs(), PULL_EXISTS] >> rpt conj_tac
      >- (simp[prefix_free_def, PULL_EXISTS] >>
          qx_genl_tac [‚Äòw1‚Äô, ‚Äòw2‚Äô, ‚Äòi‚Äô, ‚Äòpf1‚Äô, ‚Äòj‚Äô, ‚Äòpf2‚Äô] >>
          DEEP_INTRO_TAC optionTheory.some_intro >> simp[] >>
          DEEP_INTRO_TAC optionTheory.some_intro >> simp[] >>
          rw[] >> gs[] >>
          Cases_on ‚Äò(pf1,pf2) ‚àà pfunle f‚Äô
          >- (‚Äòpf2 ‚àà wfpfun f‚Äô by metis_tac[] >>
              pop_assum mp_tac >> simp_tac (srw_ss()) [wfpfun_def] >>
              simp[prefix_free_def] >>
              ‚Äòpf2 i = SOME w1 ‚àß pf2 j = SOME w2‚Äô suffices_by metis_tac[] >>
              pop_assum mp_tac >>
              simp_tac (srw_ss()) [pfunle_def] >> simp[]) >>
          first_x_assum $ qspecl_then [‚Äòpf1‚Äô, ‚Äòpf2‚Äô] mp_tac >> simp[] >>
          simp[pfunle_def] >> first_x_assum $ qspec_then ‚Äòpf1‚Äô mp_tac >>
          simp[wfpfun_def, prefix_free_def] >> rw[] >> first_x_assum irule >>
          metis_tac[optionTheory.NOT_NONE_SOME])
      >- (rpt gen_tac >> DEEP_INTRO_TAC optionTheory.some_intro >> simp[] >>
          rw[]
          >- (first_x_assum $ qspec_then ‚Äòpf‚Äô mp_tac >> simp[wfpfun_def]) >>
          DEEP_INTRO_TAC optionTheory.some_intro >> simp[] >> rw[]
          >- (rename [‚Äòpf' ‚àà t‚Äô, ‚Äòpf' j ‚â† NONE‚Äô, ‚Äòpf' j = SOME _‚Äô,
                      ‚Äòf j < LENGTH p‚Äô] >>
              Cases_on ‚Äòpf' j‚Äô >> gs[]) >>
          qexists_tac ‚Äòpf‚Äô >> simp[] >>
          first_x_assum $ qspec_then ‚Äòpf‚Äô mp_tac >> simp[wfpfun_def] >>
          metis_tac[optionTheory.NOT_NONE_SOME]) >>
      qx_genl_tac [‚Äòi‚Äô, ‚Äòj‚Äô] >>
      DEEP_INTRO_TAC optionTheory.some_intro >> simp[] >>
      DEEP_INTRO_TAC optionTheory.some_intro >> simp[] >> qx_gen_tac ‚Äòpf‚Äô >>
      Cases_on ‚Äòpf ‚àà t‚Äô >> simp[] >> Cases_on ‚Äòpf j‚Äô >> simp[] >>
      Cases_on ‚Äòf i = f j‚Äô >> simp[] >> strip_tac >>
      first_x_assum drule >> simp[wfpfun_def] >> rpt strip_tac >>
      first_x_assum (drule_at (Pos (el 2))) >> simp[] >> metis_tac[]) >>
  qx_gen_tac ‚Äòpp‚Äô >> Cases_on ‚Äòpp ‚àà t‚Äô >> simp[] >>
  simp[pfunle_def] >> qx_gen_tac ‚Äòi‚Äô >> Cases_on ‚Äòpp i‚Äô >>
  simp[AllCaseEqs()] >> DEEP_INTRO_TAC optionTheory.some_intro >> simp[] >>
  conj_tac
  >- (qx_gen_tac ‚Äòpf‚Äô >> Cases_on ‚Äòpf i‚Äô >> simp[] >> strip_tac >>
      first_x_assum $ qspecl_then [‚Äòpp‚Äô, ‚Äòpf‚Äô] mp_tac >> simp[] >>
      rw[pfunle_def] >>
      metis_tac[optionTheory.NOT_NONE_SOME, optionTheory.SOME_11]) >>
  qexists_tac ‚Äòpp‚Äô >> simp[]
QED

Theorem partial_wfpfun_FINITE:
  (‚àÄn. seq_size f n ‚â§ 1) ‚àß pf ‚àà wfpfun f ‚àß pf i = NONE ‚áí
  FINITE { j | pf j ‚â† NONE }
Proof
  simp[wfpfun_def] >> strip_tac >>
  qabbrev_tac ‚Äòmxlen = f i‚Äô >>
  ‚Äò‚àÄj p. pf j = SOME p ‚áí LENGTH p ‚â§ mxlen‚Äô
    by (CCONTR_TAC >> gs[NOT_LESS_EQUAL] >>
        ‚Äò‚àÉq. pf i = SOME q‚Äô by metis_tac[] >> fs[]) >>
  CCONTR_TAC >>
  ‚Äò‚àÉlen. INFINITE {i | f i = len}‚Äô suffices_by metis_tac[seq_size_indices] >>
  CCONTR_TAC >> gs[] >>
  ‚Äò{ j | pf j ‚â† NONE } ‚äÜ
   BIGUNION (IMAGE (Œªn. { i | f i = n }) (count (mxlen + 1)))‚Äô
    by (simp[SUBSET_DEF, PULL_EXISTS] >> qx_gen_tac ‚Äòk‚Äô >> Cases_on ‚Äòpf k‚Äô >>
        simp[GSYM LE_LT1] >> metis_tac[]) >>
  qpat_x_assum ‚ÄòINFINITE _‚Äô mp_tac >> simp[] >>
  irule SUBSET_FINITE >>
  pop_assum (goal_assum o resolve_then (Pos last) mp_tac) >>
  simp[PULL_EXISTS]
QED

Definition TN2BL_def:
  TN2BL n = MAP ((=) 1) $ REVERSE $ num_to_bin_list n
End

Definition padL_def:
  padL n bl = Fpow (n - LENGTH bl) ++ bl
End

Theorem TBL2N_padL[simp]:
  TBL2N (padL n bl) = TBL2N bl
Proof
  simp[padL_def]
QED

Theorem TBL2N_TN2BL[simp]:
  TBL2N (TN2BL n) = n
Proof
  simp[TN2BL_def, numposrepTheory.num_to_bin_list_def] >>
  completeInduct_on ‚Äòn‚Äô >> simp[Once numposrepTheory.n2l_def] >> rw[]
  >- (Cases_on ‚Äòn = 1‚Äô >> simp[]) >>
  simp[TBL2N_append] >> Cases_on ‚Äòn MOD 2 = 1‚Äô >> simp[]
  >- metis_tac[DIVISION, DECIDE ‚Äú0n < 2‚Äù, MULT_COMM] >>
  simp[bitTheory.DIV_MULT_THM2] >> ‚Äòn MOD 2 = 0‚Äô suffices_by simp[] >>
  ‚Äòn MOD 2 < 2‚Äô by simp[] >> simp[]
QED

Definition genpf_def[simp]:
  genpf [] = (0n,1n,K NONE) ‚àß
  genpf ((width,i:num)::rest) =
  let
    (n,ld,A) = genpf rest;
    code_n = n * 2 ** (width - ld);
    code = padL width (TN2BL code_n);
  in
    (n * 2 ** (width - ld) + 1, width, A‚¶ái ‚Ü¶ SOME code‚¶à)
End

(*Theorem kraft_finite_indexset:
  ALL_DISTINCT (MAP SND ixs) ‚àß
  FOLDR (Œª(wdth,_) A. A + 2 rpow -&wdth) 0r ixs < 1 ‚àß
  SORTED (inv ((<) LEX (<))) ixs ‚áí
  let (n,ld,A) = genpf ixs
  in
    (ld = if ixs = [] then 1n else FST (HD ixs)) ‚àß n < 2 ** ld ‚àß
    (‚àÄwdth i. MEM (wdth,i) ixs ‚áí ‚àÉw. A i = SOME w ‚àß LENGTH w = wdth) ‚àß
    FOLDR (Œª(wdth,_) A. A + 2 rpow -&wdth) 0r ixs = &n / 2 pow &ld
Proof
  Induct_on ‚Äòixs‚Äô >> simp[pairTheory.FORALL_PROD, MEM_MAP, PULL_EXISTS] >>
  qx_genl_tac [‚Äòw1‚Äô, ‚Äòi1‚Äô] >> strip_tac >>
  drule_then assume_tac sortingTheory.SORTED_TL >> gs[] >>
  tmCases_on ‚Äúgenpf ixs‚Äù ["n ld A"] >> gs[] >>
  simp[powr_negexp, real_div, REAL_LDISTRIB] >>
  gs[powr_negexp] >>
  qabbrev_tac ‚Äòp = if ixs = [] then 1 else FST (HD ixs)‚Äô >>
  qabbrev_tac ‚Äòfsum = FOLDR (Œª(wdth,_) A:real. A + inv (2 pow wdth)) 0 ixs‚Äô

rw[]

conj_

tac
  >- (
  >- (rw[]
      >- (simp[combinTheory.APPLY_UPDATE_THM] >>
          ‚Äòld ‚â§ w2‚Äô
          by (Cases_on ‚Äòt‚Äô >> gs[] >> rename

*)

(*
Theorem kraft_infinite_ineq2:
  ‚àÄf. (‚àÄn. seq_size f n ‚â§ 1) ‚áí
      ‚àÉP b.
        prefix_free P ‚àß BIJ b ùïå(:num) P  ‚àß ‚àÄi. LENGTH (b i) = f i
Proof
  cheat
QED

Theorem kraft_finite_ineq2:
  FOLDR (Œªn A. A + 2 rpow -&n) 0r ns ‚â§ 1 ‚áí
  ‚àÉP b.
     prefix_free P ‚àß BIJ b (count (LENGTH ns)) P ‚àß
     ‚àÄi. i < LENGTH ns ‚áí LENGTH (b i) = EL i ns
Proof
  cheat
QED
*)




val _ = export_theory();
